(context Spp)

(rule TOP     :{ ^ ( <.ws> || <atom> )+ $ })
(rule ws      :{ ( \s+ || <.comment> || <.pod> )+ })
(rule comment :{ \# [^\n]* $$ })
(rule pod     :{ ^^ '=pod' .*? ( ^^ '=end' $$ ) })
(rule atom :{
      || <int>
      || <char>
      || <Str>
      || <String>
      || <Hash>
      || <Array>
      || <List>
      || <Rule>
      || <Keyword>
      || <Sarray>
      || <dot>
      || <sym>
   })
(rule int     :{ \-? \d+ })
(rule char    :{ \\ . })
(rule Str     :{ \' ( <sstr> || <char> )* \' })
(rule Keyword :{ \: <kstr> })
(rule dot     :{ \. })
(rule sstr    :{ [^ \\ \']+ })
(rule String  :{ \" ( <dstr> || <inter> || <char> )* \" })
(rule dstr    :{ [^ \\ \" \{]+ })
(rule inter   :{ \{ ( <.ws> || <atom> )* \} })
(rule List    :{ \( ( <.ws> || \,+ || <atom> )* \) })
(rule Array   :{ \[ ( <.ws> || \,+ || <atom> )* \] })
(rule Hash    :{ \{ ( <.ws> || \,+ || <Pair> )* \} })
(rule Pair    :{ <atom> <.ws> ( '=>' <.ws> )? <atom> })
(rule sym     :{ [_a-zA-Z+\-*%=><?!~$@^&|:/]+ })
(rule kstr    :{ [_a-zA-Z0-9\-$@%<>.&|!*?+~=:^/]+ })
(rule Sarray  :{ ':[' ( <blank> || <cstr> || <char> )+ \] })
(rule cstr    :{ [^ \\ \] \s]+ })
(rule blank   :{ \s+ })
(rule Rule    :{ ':{' ( <.ws> || <ratom> )* \} })
(rule ratom   :{
      || <Group>
      || <look>
      || <rept>
      || <any>
      || <branch>
      || <lbranch>
      || <cclass>
      || <Alias>
      || \< <ctoken> \>
      || '<.' <rtoken> \>
      || '<!' <gtoken> \>
      || <Chclass>
      || <Strs>
      || <assert>
      || <Action>
      || <Str>
      || <char>
      || <chars>
   })
(rule Group   :{ \( ( <.ws> || <ratom> )* \) })
(rule look    :{ < +? *? > })
(rule rept    :{ < ? + * > })
(rule any     :{ \. })
(rule branch  :{ '||' })
(rule lbranch :{ \| })
(rule cclass  :{ \\ [dswDSW] })
(rule ctoken  :{ <.name> })
(rule rtoken  :{ <.name> })
(rule gtoken  :{ <.name> })
(rule name    :{ [a-zA-Z_] [a-zA-Z_\-:]* })
(rule Alias   :{ '$<' <name> '>=' <ratom> })
(rule Strs    :{ \< \s+ ( <str> || \s+ )+ \> })
(rule str     :{ [^ \> \s]+ })
(rule Chclass :{ \[ <flip>? ( <.ws> || <catom> )+ \] })
(rule flip    :{ \^ })
(rule catom   :{ <cclass> || <char> || <range> || <cchar> })
(rule range   :{ \S \- \S })
(rule cchar   :{ [^ \\ \] \s] })
(rule assert  :{ < ^^ $$ ^ $ > })
(rule Action  :{ \{ ( <.ws> || <atom> )* \} })
(rule chars   :{ <.name> })

(end Spp)
