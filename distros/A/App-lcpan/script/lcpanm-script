#!perl

# NO_PERINCI_CMDLINE_SCRIPT
# FRAGMENT id=shcompgen-hint command=_cpanm-script

use strict;
use warnings;

use App::lcpan;
use Perinci::CmdLine::Util::Config;

our $AUTHORITY = 'cpan:PERLANCAR'; # AUTHORITY
our $DATE = '2023-09-26'; # DATE
our $DIST = 'App-lcpan'; # DIST
our $VERSION = '1.074'; # VERSION

my $res = Perinci::CmdLine::Util::Config::read_config(
    program_name => "lcpan",
);
die "Can't read lcpan config files: $res->[0] - $res->[1]\n"
    unless $res->[0] == 200;
my $config = $res->[2];

my %args;
$res = Perinci::CmdLine::Util::Config::get_args_from_config(
    config => $config,
    args   => \%args,
    subcommand_name => 'update-index',
    meta   => $App::lcpan::SPEC{update},
);
die "Can't set lcpan function argument from config: $res->[0] - $res->[1]\n"
    unless $res->[0] == 200;
App::lcpan::_set_args_default(\%args);
my $dbh = App::lcpan::_connect_db('ro', $args{cpan}, $args{index_name}, $args{use_bootstrap});

my %modnames; # key = scriptname
{
    for (@ARGV) {
        next unless /\A[A-Za-z0-9_][A-Za-z0-9_-]*\z/;
        $modnames{$_} = undef;
    }
    my $scripts_s = join(",",map {"'$_'"} sort keys %modnames);
    last unless length $scripts_s;
    my $sth = $dbh->prepare("
SELECT
  script.name script,
  (SELECT name FROM module WHERE file_id=file.id LIMIT 1) module
FROM script
LEFT JOIN file   ON script.file_id=file.id
WHERE script.name IN ($scripts_s)
");
    $sth->execute;
    while (my @row = $sth->fetchrow_array) {
        $modnames{$row[0]} = $row[1];
    }
}

{
    local $ENV{PERL5OPT} = $ENV{LCPANM_PERL5OPT}
        if defined $ENV{LCPANM_PERL5OPT};

    my @cmd = ("cpanm");
    push @cmd, "--mirror", $args{cpan}, "--mirror-only" if $args{cpan}; # should be defined
    push @cmd, map { exists $modnames{$_} ? $modnames{$_} : $_ } @ARGV;

    print "lcpanm: exec: ".join(" ", @cmd), "\n" if $ENV{DEBUG};
    exec @cmd;
}

# ABSTRACT: cpanm wrapper (set mirror to local CPAN by default, convert script names to module names)
# PODNAME: lcpanm-script

__END__

=pod

=encoding UTF-8

=head1 NAME

lcpanm-script - cpanm wrapper (set mirror to local CPAN by default, convert script names to module names)

=head1 VERSION

This document describes version 1.074 of lcpanm-script (from Perl distribution App-lcpan), released on 2023-09-26.

=head1 SYNOPSIS

Use like you would use L<cpanm-script>:

 % lcpanm-script -n somescript ...

=head1 DESCRIPTION

C<lcpanm-script> is just like L<cpanm-script>, but also adds C<--mirror $cpan>
and C<--mirror-only>, where I<$cpan> is path to the local CPAN mirror read from
F<lcpan.conf>.

=head1 ENVIRONMENT

=head2 LCPANM_PERL5OPT

If set, will set C<PERL5OPT> to this value when executing L<cpanm>.

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-lcpan>.

=head1 SOURCE

Source repository is at L<https://github.com/perlancar/perl-App-lcpan>.

=head1 SEE ALSO

L<cpanm-script>, L<cpanm>

L<App::lcpan> and L<lcpan>

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 CONTRIBUTING


To contribute, you can send patches by email/via RT, or send pull requests on
GitHub.

Most of the time, you don't need to build the distribution yourself. You can
simply modify the code, then test via:

 % prove -l

If you want to build the distribution (e.g. to try to install it locally on your
system), you can install L<Dist::Zilla>,
L<Dist::Zilla::PluginBundle::Author::PERLANCAR>,
L<Pod::Weaver::PluginBundle::Author::PERLANCAR>, and sometimes one or two other
Dist::Zilla- and/or Pod::Weaver plugins. Any additional steps required beyond
that are considered a bug and can be reported to me.

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2023, 2022, 2021, 2020, 2019, 2018, 2017, 2016, 2015 by perlancar <perlancar@cpan.org>.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-lcpan>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=cut
