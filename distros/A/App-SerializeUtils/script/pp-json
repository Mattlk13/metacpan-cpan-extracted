#!perl

our $DATE = '2020-04-10'; # DATE
our $VERSION = '0.162'; # VERSION

use 5.010001;
use strict;
use warnings;

use Getopt::Long;

my $color = $ENV{COLOR} // -t STDOUT;
my $pretty = 1;
my $mod = '';
GetOptions(
    'pretty!' => \$pretty,
    'color!' => \$color,
    'pp' => sub { $mod = 'pp' },
    'xs' => sub { $mod = 'xs' },
    'help' => sub {
        print "Usage: pp-json [OPTIONS] <file>\n";
        exit 0;
    },
);

undef $/;

my $json;
my $res;
if ($mod eq 'pp') {
    require JSON::PP; $json = JSON::PP->new;
    $json->canonical->allow_nonref->pretty($pretty);
    $res = $json->encode($json->decode(scalar <>));
} elsif ($mod eq 'xs') {
    require Cpanel::JSON::XS; $json = Cpanel::JSON::XS->new;
    $json->canonical->allow_nonref->pretty($pretty);
    $res = $json->encode($json->decode(scalar <>));
} else {
    require JSON::MaybeXS; $json = JSON::MaybeXS->new;
    $json->canonical->allow_nonref->pretty($pretty);
    if ($color) {
        require JSON::Color;
        $res = JSON::Color::encode_json($json->decode(scalar <>), {pretty=>$pretty});
    } else {
        $res = $json->encode($json->decode(scalar <>));
    }
}
print $res;
print "\n" if (-t STDOUT) && $res !~ /\R\z/;

1;
# ABSTRACT: Pretty-print (and colorize) JSON
# PODNAME: pp-json

__END__

=pod

=encoding UTF-8

=head1 NAME

pp-json - Pretty-print (and colorize) JSON

=head1 VERSION

This document describes version 0.162 of pp-json (from Perl distribution App-SerializeUtils), released on 2020-04-10.

=head1 DESCRIPTION

By default, when in interactive mode will pretty-print and colorize JSON using
L<JSON::Color>, and when piped will pretty-print JSON using L<JSON::MaybeXS>.

=head1 OPTIONS

=over

=item * --color, --no-color

Force using color (JSON::Color) or no color. The default is to check whether
STDOUT is not piped (interactive mode) to enable color.

=item * --no-pretty

Instead of pretty-printing, print compact JSON.

=item * --pp

Force using a PP module (currently L<JSON::PP>) instead of the default
L<JSON::MaybeXS>.

=item * --xs

Force using an XS module (currently L<Cpanel::JSON::XS>) instead of
the default L<JSON::MaybeXS>.

=back

=head1 ENVIRONMENT

=head2 COLOR

Boolean. Set default for color.

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-SerializeUtils>.

=head1 SOURCE

Source repository is at L<https://github.com/perlancar/perl-App-SerializeUtils>.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-SerializeUtils>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2020, 2018, 2017, 2015, 2014, 2013, 2011 by perlancar@cpan.org.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
