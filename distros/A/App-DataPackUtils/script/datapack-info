#!perl

our $DATE = '2020-04-30'; # DATE
our $VERSION = '0.06'; # VERSION

use 5.010;
use strict;
use warnings;

use ExtUtils::MakeMaker;
use Perinci::CmdLine::Any;

our %SPEC;
$SPEC{datapack_info} = {
    v => 1.1,
    summary => 'Given a datapacked script, show various info',
    args => {
        script => {
            schema => 'filename*',
            req => 1,
            pos => 0,
            completion => sub {
                require Complete::Program;

                my %args = @_;
                my $word = $args{word};

                Complete::Program::complete_program(word => $word);
            },
        },
        list_modules => {
            schema => ['bool' => is=>1],
            cmdline_aliases => {l=>{}},
            tags => ['category:action'],
        },
        show_module_version => {
            schema => ['bool' => is=>1],
            cmdline_aliases => {V=>{}},
        },
    },
    args_rels => {
        dep_all => [show_module_version => ['list_modules']],
    },
    examples => [
        {
            summary => 'Show total size of script and datapack contents',
            src => 'datapack-info /tmp/script.packed',
            src_plang => 'bash',
            test => 0,
            'x.doc.show_result' => 0,
        },
        {
            summary => 'Show list of datapacked modules (-l) along with their versions (-V)',
            src => 'datapack-info -lV `which datapacked-script`',
            src_plang => 'bash',
            test => 0,
            'x.doc.show_result' => 0,
        },
    ],
};
sub datapack_info {
    require File::Which;

    my %args = @_;

    my $script = $args{script};
    unless (-f $script) {
        $script = File::Which::which($script);
        return [400, "No such script '$script'"] unless $script;
    }
    open my($fh), "<", $script
        or return [500, "Can't open script '$script': $!"];

    my $found;
    while (<$fh>) {
        chomp;
        do {$found++; last} if /\A__DATA__\z/;
    }
    return [412, "No __DATA__ found in script"] unless $found;

    require Data::Section::Seekable::Reader;
    my $reader = Data::Section::Seekable::Reader->new(handle=>$fh);

    my @parts;
    my $temp_filename;
    my %modules;
    for my $part ($reader->parts) {
        my $ct0 = $reader->read_part($part);
        my $ct = $ct0; $ct =~ s/^#//gm;
        $modules{$part} = {};
        if ($args{show_module_version}) {
            require File::Slurper;
            require File::Temp;
            unless ($temp_filename) {
                (undef, $temp_filename) = File::Temp::tempfile();
            }
            File::Slurper::write_text($temp_filename, $ct);
            $modules{$part}{version} = MM->parse_version($temp_filename);
        }
        push @parts, {
            name => $part,
            length => length($ct),
        };
    }

    my $res;
    if ($args{list_modules}) {
        $res = [];
        for my $k (sort keys %modules) {
            my $mod = $k; $mod =~ s/\.pm$//; $mod =~ s!/!::!g;
            if ($args{show_module_version}) {
                push @$res, "$mod\t" . $modules{$k}{version};
            } else {
                push @$res, $mod;
            }
        }
    } else {
        $res = {
            file_size => (-s $script),
            parts => \@parts,
        };
    };

    [200, "OK", $res];
}

Perinci::CmdLine::Any->new(
    url => '/main/datapack_info',
)->run;

# ABSTRACT: Given a datapacked script, show various info
# PODNAME: datapack-info

__END__

=pod

=encoding UTF-8

=head1 NAME

datapack-info - Given a datapacked script, show various info

=head1 VERSION

This document describes version 0.06 of datapack-info (from Perl distribution App-DataPackUtils), released on 2020-04-30.

=head1 SYNOPSIS

Usage:

 % datapack-info [-V] [--config-path=path | -c] [--config-profile=profile |
     -P] [--format=name] [--json] [-l] [--list-modules] [--(no)naked-res]
     [--no-config | -C] [--no-env] [--page-result[=program]]
     [--show-module-version] <script>

Examples:

Show total size of script and datapack contents:

 % datapack-info /tmp/script.packed

Show list of datapacked modules (-l) along with their versions (-V):

 % datapack-info -lV `which datapacked-script`

=head1 OPTIONS

C<*> marks required options.

=head2 Main options

=over

=item B<--script>=I<s>*

=item B<--show-module-version>, B<-V>

=back

=head2 Action options

=over

=item B<--list-modules>, B<-l>

=back

=head2 Configuration options

=over

=item B<--config-path>=I<s>, B<-c>

Set path to configuration file.

=item B<--config-profile>=I<s>, B<-P>

Set configuration profile to use.

=item B<--no-config>, B<-C>

Do not use any configuration file.

=back

=head2 Environment options

=over

=item B<--no-env>

Do not read environment for default options.

=back

=head2 Output options

=over

=item B<--format>=I<s>

Choose output format, e.g. json, text.

Default value:

 undef

=item B<--json>

Set output format to json.

=item B<--naked-res>

When outputing as JSON, strip result envelope.

Default value:

 0

By default, when outputing as JSON, the full enveloped result is returned, e.g.:

    [200,"OK",[1,2,3],{"func.extra"=>4}]

The reason is so you can get the status (1st element), status message (2nd
element) as well as result metadata/extra result (4th element) instead of just
the result (3rd element). However, sometimes you want just the result, e.g. when
you want to pipe the result for more post-processing. In this case you can use
`--naked-res` so you just get:

    [1,2,3]


=item B<--page-result>

Filter output through a pager.

=back

=head2 Other options

=over

=item B<--help>, B<-h>, B<-?>

Display help message and exit.

=item B<--version>, B<-v>

Display program's version and exit.

=back

=head1 COMPLETION

This script has shell tab completion capability with support for several
shells.

=head2 bash

To activate bash completion for this script, put:

 complete -C datapack-info datapack-info

in your bash startup (e.g. F<~/.bashrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is recommended, however, that you install modules using L<cpanm-shcompgen>
which can activate shell completion for scripts immediately.

=head2 tcsh

To activate tcsh completion for this script, put:

 complete datapack-info 'p/*/`datapack-info`/'

in your tcsh startup (e.g. F<~/.tcshrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is also recommended to install L<shcompgen> (see above).

=head2 other shells

For fish and zsh, install L<shcompgen> as described above.

=head1 CONFIGURATION FILE

This script can read configuration files. Configuration files are in the format of L<IOD>, which is basically INI with some extra features.

By default, these names are searched for configuration filenames (can be changed using C<--config-path>): F<~/.config/datapack-info.conf>, F<~/datapack-info.conf>, or F</etc/datapack-info.conf>.

All found files will be read and merged.

To disable searching for configuration files, pass C<--no-config>.

You can put multiple profiles in a single file by using section names like C<[profile=SOMENAME]> or C<[SOMESECTION profile=SOMENAME]>. Those sections will only be read if you specify the matching C<--config-profile SOMENAME>.

You can also put configuration for multiple programs inside a single file, and use filter C<program=NAME> in section names, e.g. C<[program=NAME ...]> or C<[SOMESECTION program=NAME]>. The section will then only be used when the reading program matches.

Finally, you can filter a section by environment variable using the filter C<env=CONDITION> in section names. For example if you only want a section to be read if a certain environment variable is true: C<[env=SOMEVAR ...]> or C<[SOMESECTION env=SOMEVAR ...]>. If you only want a section to be read when the value of an environment variable has value equals something: C<[env=HOSTNAME=blink ...]> or C<[SOMESECTION env=HOSTNAME=blink ...]>. If you only want a section to be read when the value of an environment variable does not equal something: C<[env=HOSTNAME!=blink ...]> or C<[SOMESECTION env=HOSTNAME!=blink ...]>. If you only want a section to be read when an environment variable contains something: C<[env=HOSTNAME*=server ...]> or C<[SOMESECTION env=HOSTNAME*=server ...]>. Note that currently due to simplistic parsing, there must not be any whitespace in the value being compared because it marks the beginning of a new section filter or section name.

List of available configuration parameters:

 format (see --format)
 list_modules (see --list-modules)
 naked_res (see --naked-res)
 script (see --script)
 show_module_version (see --show-module-version)

=head1 ENVIRONMENT

=head2 DATAPACK_INFO_OPT => str

Specify additional command-line options.

=head1 FILES

F<~/.config/datapack-info.conf>

F<~/datapack-info.conf>

F</etc/datapack-info.conf>

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-DataPackUtils>.

=head1 SOURCE

Source repository is at L<https://github.com/perlancar/perl-App-DataPackUtils>.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-DataPackUtils>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2020, 2017, 2016, 2015 by perlancar@cpan.org.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
