#!perl

our $DATE = '2017-02-01'; # DATE
our $VERSION = '0.67'; # VERSION

use 5.010;
use strict;
use warnings;
use Log::Any::IfLOG '$log';

use App::PMUtils;
use Perinci::CmdLine::Any;

our %SPEC;
$SPEC{pmminversion} = {
    v => 1.1,
    summary => 'Get minimum required Perl version for specified modules',
    description => <<'_',
_
    args => {
        module => $App::PMUtils::arg_module_multiple,
        detector => {
            schema => ['str*', in=>[qw/Perl::MinimumVersion Perl::MinimumVersion::Fast/]],
            default => 'Perl::MinimumVersion',
        },
        #with_minimum_external_version => {
        #    schema => 'bool',
        #},
        with_minimum_syntax_version => {
            schema => 'bool',
        },
        with_minimum_explicit_version => {
            schema => 'bool',
        },
    },
};
sub pmminversion {
    require Module::Path::More;

    my %args = @_;
    my $mods = $args{module};
    my $det  = $args{detector} // 'Perl::MinimumVersion';

    my $found;
    my $res = [];
    for my $mod (@$mods) {
        $mod =~ s!/!::!g;
        my $mpath = Module::Path::More::module_path(module=>$mod);
        unless ($mpath) {
            warn "Module $mod is not installed";
            next;
        }
        $found++;

        my $pmv;
        if ($det eq 'Perl::MinimumVersion::Fast') {
            require Perl::MinimumVersion::Fast;
            $pmv = Perl::MinimumVersion::Fast->new($mpath);
        } else {
            require Perl::MinimumVersion;
            $pmv = Perl::MinimumVersion->new($mpath);
        }

        my $v;
        my $rec = {
            module => $mod,
        };
        $v = $pmv->minimum_version;
        $rec->{minimum_version} = "$v";
        if ($args{with_minimum_explicit_version}) {
            $rec->{minimum_explicit_version} = $pmv->minimum_explicit_version;
        }
        if ($args{with_minimum_syntax_version}) {
            $rec->{minimum_syntax_version} = $pmv->minimum_syntax_version;
        }

        push @$res, $rec;
    }

    if (!$found) {
        [404, "No such module(s): " . join(", ", @$mods)];
    } else {
        [200, "OK", $res, {
            'table.fields' => [qw/module minimum_version minimum_explicit_version minimum_syntax_version/],
        }];
    }
}

Perinci::CmdLine::Any->new(
    url => '/main/pmminversion',
)->run;

# ABSTRACT: Get minimum required Perl version for specified modules
# PODNAME: pmminversion

__END__

=pod

=encoding UTF-8

=head1 NAME

pmminversion - Get minimum required Perl version for specified modules

=head1 VERSION

This document describes version 0.67 of pmminversion (from Perl distribution App-PMUtils), released on 2017-02-01.

=head1 SYNOPSIS

 % pmminversion Some::Module Another::Module

=head1 OPTIONS

C<*> marks required options.

=head2 Main options

=over

=item B<--detector>=I<s>

Default value:

 "Perl::MinimumVersion"

Valid values:

 ["Perl::MinimumVersion","Perl::MinimumVersion::Fast"]

=item B<--module-json>=I<s>

See C<--module>.

=item B<--module>=I<s@>*

Can be specified multiple times.

=item B<--with-minimum-explicit-version>

=item B<--with-minimum-syntax-version>

=back

=head2 Configuration options

=over

=item B<--config-path>=I<filename>

Set path to configuration file.

Can be specified multiple times.

=item B<--config-profile>=I<s>

Set configuration profile to use.

=item B<--no-config>

Do not use any configuration file.

=back

=head2 Environment options

=over

=item B<--no-env>

Do not read environment for default options.

=back

=head2 Output options

=over

=item B<--format>=I<s>

Choose output format, e.g. json, text.

Default value:

 undef

=item B<--json>

Set output format to json.

=item B<--naked-res>

When outputing as JSON, strip result envelope.

Default value:

 0

By default, when outputing as JSON, the full enveloped result is returned, e.g.:

    [200,"OK",[1,2,3],{"func.extra"=>4}]

The reason is so you can get the status (1st element), status message (2nd
element) as well as result metadata/extra result (4th element) instead of just
the result (3rd element). However, sometimes you want just the result, e.g. when
you want to pipe the result for more post-processing. In this case you can use
`--naked-res` so you just get:

    [1,2,3]


=back

=head2 Other options

=over

=item B<--help>, B<-h>, B<-?>

Display help message and exit.

=item B<--version>, B<-v>

Display program's version and exit.

=back

=head1 COMPLETION

The script comes with a companion shell completer script (L<pmminversion>)
for this script.

=head2 bash

To activate bash completion for this script, put:

 complete -C pmminversion pmminversion

in your bash startup (e.g. F<~/.bashrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is recommended, however, that you install L<shcompgen> which allows you to
activate completion scripts for several kinds of scripts on multiple shells.
Some CPAN distributions (those that are built with
L<Dist::Zilla::Plugin::GenShellCompletion>) will even automatically enable shell
completion for their included scripts (using L<shcompgen>) at installation time,
so you can immediately have tab completion.

=head2 tcsh

To activate tcsh completion for this script, put:

 complete pmminversion 'p/*/`pmminversion`/'

in your tcsh startup (e.g. F<~/.tcshrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is also recommended to install L<shcompgen> (see above).

=head2 other shells

For fish and zsh, install L<shcompgen> as described above.

=head1 CONFIGURATION FILE

This script can read configuration files. Configuration files are in the format of L<IOD>, which is basically INI with some extra features.

By default, these names are searched for configuration filenames (can be changed using C<--config-path>): F<~/.config/pmminversion.conf>, F<~/pmminversion.conf>, or F</etc/pmminversion.conf>.

All found files will be read and merged.

To disable searching for configuration files, pass C<--no-config>.

You can put multiple profiles in a single file by using section names like C<[profile=SOMENAME]> or C<[SOMESECTION profile=SOMENAME]>. Those sections will only be read if you specify the matching C<--config-profile SOMENAME>.

You can also put configuration for multiple programs inside a single file, and use filter C<program=NAME> in section names, e.g. C<[program=NAME ...]> or C<[SOMESECTION program=NAME]>. The section will then only be used when the reading program matches.

Finally, you can filter a section by environment variable using the filter C<env=CONDITION> in section names. For example if you only want a section to be read if a certain environment variable is true: C<[env=SOMEVAR ...]> or C<[SOMESECTION env=SOMEVAR ...]>. If you only want a section to be read when the value of an environment variable has value equals something: C<[env=HOSTNAME=blink ...]> or C<[SOMESECTION env=HOSTNAME=blink ...]>. If you only want a section to be read when the value of an environment variable does not equal something: C<[env=HOSTNAME!=blink ...]> or C<[SOMESECTION env=HOSTNAME!=blink ...]>. If you only want a section to be read when an environment variable contains something: C<[env=HOSTNAME*=server ...]> or C<[SOMESECTION env=HOSTNAME*=server ...]>. Note that currently due to simplistic parsing, there must not be any whitespace in the value being compared because it marks the beginning of a new section filter or section name.

List of available configuration parameters:

 detector (see --detector)
 format (see --format)
 module (see --module)
 naked_res (see --naked-res)
 with_minimum_explicit_version (see --with-minimum-explicit-version)
 with_minimum_syntax_version (see --with-minimum-syntax-version)

=head1 ENVIRONMENT

=head2 PMMINVERSION_OPT => str

Specify additional command-line options.

=head1 FILES

F<~/.config/pmminversion.conf>

F<~/pmminversion.conf>

F</etc/pmminversion.conf>

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-PMUtils>.

=head1 SOURCE

Source repository is at L<https://github.com/sharyanto/perl-App-PMUtils>.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-PMUtils>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=head1 SEE ALSO

L<Perl::MinimumVersion>, L<perlver>

L<Perl::MinimumVersion::Fast>

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2017 by perlancar@cpan.org.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
