#!perl

use 5.010;
use strict;
use warnings;
use Log::ger;

use App::PMUtils;
use Perinci::CmdLine::Any;

our $AUTHORITY = 'cpan:PERLANCAR'; # AUTHORITY
our $DATE = '2022-09-11'; # DATE
our $DIST = 'App-PMUtils'; # DIST
our $VERSION = '0.741'; # VERSION

our %SPEC;
$SPEC{pmcost} = {
    v => 1.1,
    summary => 'Load Perl module with Devel::EndStats',
    description => <<'_',



_
    args => {
        module => {
            schema => ['array*', of=>'perl::modname_with_optional_args*'],
            req => 1,
            pos => 0,
            slurpy => 1,
        },
        import => {
            summary => 'Load module with default exports: use MOD',
            'summary.alt.bool.not' => 'Load module without import(): use MOD ()',
            schema => 'bool*',
            default => 1,
        },
        hide_core => {
            schema => 'bool',
        },
        sort => {
            schema => ['str*', in=>[map {($_,"-$_")}
                                        qw/file time caller order lines/]],
        },
        hide_noncore => {
            schema => 'bool',
        },
        show_memsize => {
            schema => 'bool',
        },
    },
    examples => [
        {test=>0, 'x.doc.show_result'=>0, src=>"pmcost Perinci::CmdLine", src_plang=>"bash"},
        {test=>0, 'x.doc.show_result'=>0, src=>"pmcost if=1,Perinci::CmdLine", src_plang=>"bash"},
    ],
};
sub pmcost {
    my %args = @_;
    my $mods = $args{module};
    my $import = $args{import} // 1;

    my $de_import = "verbose,1";
    $de_import .= ",hide_core,1" if $args{hide_core};
    $de_import .= ",hide_noncore,1" if $args{hide_noncore};
    $de_import .= ",show_memsize,1,sort,-memsize" if $args{show_memsize};
    $de_import .= ",sort,$args{sort}" if $args{sort};
    my @cmd = ($^X, "-MDevel::EndStats=$de_import");

    for my $mod (@$mods) {
        $mod =~ s!/!::!g;
        push @cmd, $import ? "-M$mod" : "-m$mod";
    }

    push @cmd, "-e1";

    log_trace("exec(%s)", \@cmd);
    exec @cmd;
    # [200]; # won't get reached
}

Perinci::CmdLine::Any->new(
    url => '/main/pmcost',
    read_config => 0,
    read_env => 0,
)->run;

# ABSTRACT: Load Perl module with Devel::EndStats
# PODNAME: pmcost

__END__

=pod

=encoding UTF-8

=head1 NAME

pmcost - Load Perl module with Devel::EndStats

=head1 VERSION

This document describes version 0.741 of pmcost (from Perl distribution App-PMUtils), released on 2022-09-11.

=head1 SYNOPSIS

 % pmcost HTTP::Tiny

 # Start stats from Devel::EndStats:
 # Program runtime duration: 0.011s
 # Total number of required files loaded: 13
 # Total number of required lines loaded: 6789
 # Seq  Lines  Load Time         Module
 #  11     54     0.128ms(  1%)    SelectSaver.pm (loaded by IO::Handle)
 #  13     68     0.488ms(  4%)    IO.pm (loaded by IO::Handle)
 #   4    100     0.091ms(  0%)    bytes.pm (loaded by HTTP::Tiny)
 #  17    143     0.387ms(  3%)    IO/Socket/UNIX.pm (loaded by IO::Socket)
 #   9    170     0.306ms(  2%)    Symbol.pm (loaded by IO::Handle)
 #   5    283     0.368ms(  3%)    Errno.pm (loaded by HTTP::Tiny::Handle)
 #  14    355     0.226ms(  2%)    XSLoader.pm (loaded by IO)
 #  16    466     1.182ms( 10%)    IO/Socket/INET.pm (loaded by IO::Socket)
 #   6    569     5.831ms( 53%)    IO/Socket.pm (loaded by HTTP::Tiny::Handle)
 #   7    653     1.912ms( 17%)    IO/Handle.pm (loaded by IO::Socket)
 #   2    709     1.035ms(  9%)    Carp.pm (loaded by HTTP::Tiny)
 #  15   1114     1.328ms( 12%)    Socket.pm (loaded by IO::Socket)
 #   1   2105    10.889ms( 99%)    HTTP/Tiny.pm (loaded by main)
 # End of stats

=head1 DESCRIPTION

=head1 OPTIONS

C<*> marks required options.

=head2 Main options

=over

=item B<--hide-core>

=item B<--hide-noncore>

=item B<--module-json>=I<s>

See C<--module>.

Can also be specified as the 1st command-line argument and onwards.

=item B<--module>=I<s>*

Can also be specified as the 1st command-line argument and onwards.

=item B<--no-import>

Load module without import(): use MOD ().

=item B<--show-memsize>

=item B<--sort>=I<s>

Valid values:

 ["file","-file","time","-time","caller","-caller","order","-order","lines","-lines"]

=back

=head2 Output options

=over

=item B<--format>=I<s>

Choose output format, e.g. json, text.

Default value:

 undef

Output can be displayed in multiple formats, and a suitable default format is
chosen depending on the application and/or whether output destination is
interactive terminal (i.e. whether output is piped). This option specifically
chooses an output format.


=item B<--json>

Set output format to json.

=item B<--naked-res>

When outputing as JSON, strip result envelope.

Default value:

 0

By default, when outputing as JSON, the full enveloped result is returned, e.g.:

 [200,"OK",[1,2,3],{"func.extra"=>4}]

The reason is so you can get the status (1st element), status message (2nd
element) as well as result metadata/extra result (4th element) instead of just
the result (3rd element). However, sometimes you want just the result, e.g. when
you want to pipe the result for more post-processing. In this case you can use
C<--naked-res> so you just get:

 [1,2,3]


=item B<--page-result>

Filter output through a pager.

This option will pipe the output to a specified pager program. If pager program
is not specified, a suitable default e.g. C<less> is chosen.


=item B<--view-result>

View output using a viewer.

This option will first save the output to a temporary file, then open a viewer
program to view the temporary file. If a viewer program is not chosen, a
suitable default, e.g. the browser, is chosen.


=back

=head2 Other options

=over

=item B<--help>, B<-h>, B<-?>

Display help message and exit.

=item B<--version>, B<-v>

Display program's version and exit.

=back

=head1 COMPLETION

This script has shell tab completion capability with support for several
shells.

=head2 bash

To activate bash completion for this script, put:

 complete -C pmcost pmcost

in your bash startup (e.g. F<~/.bashrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is recommended, however, that you install modules using L<cpanm-shcompgen>
which can activate shell completion for scripts immediately.

=head2 tcsh

To activate tcsh completion for this script, put:

 complete pmcost 'p/*/`pmcost`/'

in your tcsh startup (e.g. F<~/.tcshrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is also recommended to install L<shcompgen> (see above).

=head2 other shells

For fish and zsh, install L<shcompgen> as described above.

=head1 EXAMPLES

=head2 Example #1

 % pmcost Perinci::CmdLine

=head2 Example #2

 % pmcost if=1,Perinci::CmdLine

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-PMUtils>.

=head1 SOURCE

Source repository is at L<https://github.com/perlancar/perl-App-PMUtils>.

=head1 SEE ALSO

L<plcost> (from L<App::PlUtils> distribution)

L<bencher-module-startup-overhead> from L<App::BencherUtils>

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 CONTRIBUTING


To contribute, you can send patches by email/via RT, or send pull requests on
GitHub.

Most of the time, you don't need to build the distribution yourself. You can
simply modify the code, then test via:

 % prove -l

If you want to build the distribution (e.g. to try to install it locally on your
system), you can install L<Dist::Zilla>,
L<Dist::Zilla::PluginBundle::Author::PERLANCAR>,
L<Pod::Weaver::PluginBundle::Author::PERLANCAR>, and sometimes one or two other
Dist::Zilla- and/or Pod::Weaver plugins. Any additional steps required beyond
that are considered a bug and can be reported to me.

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2022, 2021, 2020, 2019, 2018, 2017, 2016, 2015, 2014 by perlancar <perlancar@cpan.org>.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-PMUtils>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=cut
