#!perl

use 5.010;
use strict;
use warnings;

use App::PMUtils;
use Perinci::CmdLine::Any;

our $AUTHORITY = 'cpan:PERLANCAR'; # AUTHORITY
our $DATE = '2024-08-30'; # DATE
our $DIST = 'App-PMUtils'; # DIST
our $VERSION = '0.745'; # VERSION

our %SPEC;
$SPEC{pmchkver} = {
    v => 1.1,
    summary => "Check local module's version against version on CPAN",
    args => {
        module => $App::PMUtils::arg_module_multiple,
        detail => {
            summary => 'Show detail records intead of just module names',
            schema => 'bool',
            cmdline_aliases => {l=>{}},
        },
        latest => {
            summary => 'Only show module when it is the latest version',
            'summary.alt.bool.not' => 'Only show module when it is not the latest version',
            schema => 'bool',
            tags => ['category:filtering'],
        },
        installed => {
            summary => 'Only show module when it is installed locally',
            'summary.alt.bool.not' => 'Only show module when it is not installed locally',
            schema => 'bool',
            tags => ['category:filtering'],
        },
        on_cpan => {
            summary => 'Only show module when it is on CPAN',
            'summary.alt.bool.not' => 'Only show module when it is not on CPAN',
            schema => 'bool',
            tags => ['category:filtering'],
        },
    },
};
sub pmchkver {
    no strict 'refs'; ## no critic: TestingAndDebugging::RequireUseStrict
    require HTTP::Tiny;
    require JSON::MaybeXS;
    require Module::Path::More;

    my %args = @_;

    my $mods = $args{module};

    my @recs;
    for my $mod (@{$mods}) {
        #say "D:Checking $mod ...";
        my $rec = {
            module => $mod,
            cpan_version => undef,
            local_version => undef,
            installed => undef,
            on_cpan => undef,
            latest => undef,
        };

        my $mpath = Module::Path::More::module_path(module => $mod);
        if (!$mpath) {
            $rec->{installed} = 0;
        } else {
            $rec->{installed} = 1;
            my $mod_pm = $mod; $mod_pm =~ s!::!/!g; $mod_pm .= ".pm";
            eval { require $mod_pm };
            if ($@) {
                $rec->{note} = "Can't load local module";
            } else {
                $rec->{local_version} = ${"$mod\::VERSION"};
            }
        }
        next if defined($args{installed}) && ($args{installed} xor $rec->{installed});

        {
            my $apires = HTTP::Tiny->new->get("http://fastapi.metacpan.org/v1/module/$mod?fields=version");
            unless ($apires->{success}) {
                $rec->{note} = "Failed API request (1): $apires->{status} - $apires->{reason}";
                last;
            }
            eval { $apires = JSON::MaybeXS::decode_json($apires->{content}) };
            if ($@) {
                $rec->{note} = "Invalid API response: not valid JSON: $@";
                last;
            }
            if ($apires->{message}) {
                if ($apires->{code} == 404) {
                    $rec->{on_cpan} = 0;
                    last;
                } else {
                    $rec->{note} = "Failed API request (2): $apires->{code} - $apires->{message}";
                    last;
                }
            }
            $rec->{on_cpan} = 1;
            $rec->{cpan_version} = $apires->{version};
        }
        next if defined($args{on_cpan}) && ($args{on_cpan} xor $rec->{on_cpan});

        $rec->{latest} = defined($rec->{local_version}) &&
            defined($rec->{cpan_version}) &&
            version->parse($rec->{local_version}) >= version->parse($rec->{cpan_version}) ? 1:0;
        next if defined($args{latest}) && ($args{latest} xor $rec->{latest});

        push @recs, $rec;
    }

    [200, "OK", $args{detail} ? \@recs : [map {@recs > 1 ? [$_->{module}, $_->{latest}] : $_->{latest}} @recs],
     {
         'table.fields' => [qw/module local_version cpan_version installed on_cpan latest/],
         ('cmdline.result.interactive' => "Installed $recs[0]{module} ($recs[0]{local_version}) is ".
              ($recs[0]{latest} ? "the latest" : "NOT the latest ($recs[0]{cpan_version})")) x !!(@recs == 1),
     }];
}

Perinci::CmdLine::Any->new(
    url => '/main/pmchkver',
    read_config => 0,
    read_env => 0,
)->run;

# ABSTRACT: Check local module's version against version on CPAN
# PODNAME: pmchkver

__END__

=pod

=encoding UTF-8

=head1 NAME

pmchkver - Check local module's version against version on CPAN

=head1 VERSION

This document describes version 0.745 of pmchkver (from Perl distribution App-PMUtils), released on 2024-08-30.

=head1 SYNOPSIS

Basic usage:

 % pmchkver Some::Module Another::Module
 % pmlatest Some::Module Another::Module

Will return 1 or 0 for each module, depending on whether the corresponding
installed module is the latest version on CPAN.

When C<--detail> is given, will give more detailed information.

=head1 OPTIONS

C<*> marks required options.

=head2 Main options

=over

=item B<--detail>, B<-l>

Show detail records intead of just module names.

=item B<--module-json>=I<s>

See C<--module>.

Can also be specified as the 1st command-line argument and onwards.

=item B<--module>=I<s@>

(No description)


Can also be specified as the 1st command-line argument and onwards.

Can be specified multiple times.

=back

=head2 Filtering options

=over

=item B<--installed>

Only show module when it is installed locally.

=item B<--latest>

Only show module when it is the latest version.

=item B<--on-cpan>

Only show module when it is on CPAN.

=back

=head2 Output options

=over

=item B<--format>=I<s>

Choose output format, e.g. json, text.

Default value:

 undef

Output can be displayed in multiple formats, and a suitable default format is
chosen depending on the application and/or whether output destination is
interactive terminal (i.e. whether output is piped). This option specifically
chooses an output format.


=item B<--json>

Set output format to json.

=item B<--naked-res>

When outputing as JSON, strip result envelope.

Default value:

 0

By default, when outputing as JSON, the full enveloped result is returned, e.g.:

 [200,"OK",[1,2,3],{"func.extra"=>4}]

The reason is so you can get the status (1st element), status message (2nd
element) as well as result metadata/extra result (4th element) instead of just
the result (3rd element). However, sometimes you want just the result, e.g. when
you want to pipe the result for more post-processing. In this case you can use
C<--naked-res> so you just get:

 [1,2,3]


=item B<--page-result>

Filter output through a pager.

This option will pipe the output to a specified pager program. If pager program
is not specified, a suitable default e.g. C<less> is chosen.


=item B<--view-result>

View output using a viewer.

This option will first save the output to a temporary file, then open a viewer
program to view the temporary file. If a viewer program is not chosen, a
suitable default, e.g. the browser, is chosen.


=back

=head2 Other options

=over

=item B<--help>, B<-h>, B<-?>

Display help message and exit.

=item B<--version>, B<-v>

Display program's version and exit.

=back

=head1 COMPLETION

This script has shell tab completion capability with support for several
shells.

=head2 bash

To activate bash completion for this script, put:

 complete -C pmchkver pmchkver

in your bash startup (e.g. F<~/.bashrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is recommended, however, that you install modules using L<cpanm-shcompgen>
which can activate shell completion for scripts immediately.

=head2 tcsh

To activate tcsh completion for this script, put:

 complete pmchkver 'p/*/`pmchkver`/'

in your tcsh startup (e.g. F<~/.tcshrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is also recommended to install L<shcompgen> (see above).

=head2 other shells

For fish and zsh, install L<shcompgen> as described above.

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-PMUtils>.

=head1 SOURCE

Source repository is at L<https://github.com/perlancar/perl-App-PMUtils>.

=head1 SEE ALSO

L<Module::CheckVersion>

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 CONTRIBUTING


To contribute, you can send patches by email/via RT, or send pull requests on
GitHub.

Most of the time, you don't need to build the distribution yourself. You can
simply modify the code, then test via:

 % prove -l

If you want to build the distribution (e.g. to try to install it locally on your
system), you can install L<Dist::Zilla>,
L<Dist::Zilla::PluginBundle::Author::PERLANCAR>,
L<Pod::Weaver::PluginBundle::Author::PERLANCAR>, and sometimes one or two other
Dist::Zilla- and/or Pod::Weaver plugins. Any additional steps required beyond
that are considered a bug and can be reported to me.

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2024, 2023, 2022, 2021, 2020, 2019, 2018, 2017, 2016, 2015, 2014 by perlancar <perlancar@cpan.org>.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-PMUtils>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=cut
