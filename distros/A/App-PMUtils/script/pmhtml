#!perl

our $DATE = '2020-01-31'; # DATE
our $VERSION = '0.730'; # VERSION

use 5.010;
use strict;
use warnings;

use App::PMUtils;
use Perinci::CmdLine::Any;

our %SPEC;
$SPEC{pmhtml} = {
    v => 1.1,
    summary => 'Render POD as HTML then show in browser',
    description => <<'_',

This function renders a POD document using <prog:pod2html>, then open a browser
to show the resulting HTML document.

_
    args => {
        module => $App::PMUtils::arg_module_single,
        pod => {
            schema => 'bool',
            default => 1,
        },
    },
    deps => {
        prog => 'pod2html',
    },
};
sub pmhtml {
    require Browser::Open;
    require File::Temp;
    require File::Util::Tempdir;
    require Module::Path::More;

    my %args = @_;
    my $mod = $args{module};
    my $mpath = Module::Path::More::module_path(
        module => $mod, find_pmc=>0, find_pod=>$args{pod}, find_prefix=>0);
    return [404, "Can't find module $mod"] unless defined $mpath;

    my $tmpdir = File::Util::Tempdir::get_tempdir();
    my $cachedir = File::Temp::tempdir(CLEANUP => 1);
    my $name = $mod; $name =~ s/:+/_/g;
    my ($infh, $infile) = File::Temp::tempfile(
        "$name.XXXXXXXX", DIR=>$tmpdir, SUFFIX=>".pod");
    my $outfile = "$infile.html";
    system(
        "pod2html",
        "--infile", $mpath,
        "--outfile", $outfile,
        "--cachedir", $cachedir,
    );
    return [500, "Can't pod2html: $!"] if $?;
    my $err = Browser::Open::open_browser("file:$outfile");
    return [500, "Can't open browser"] if $err;
    [200];
}

Perinci::CmdLine::Any->new(
    url => '/main/pmhtml',
    read_config => 0,
    read_env => 0,
)->run;

# ABSTRACT: Render POD as HTML then show in browser
# PODNAME: pmhtml

__END__

=pod

=encoding UTF-8

=head1 NAME

pmhtml - Render POD as HTML then show in browser

=head1 VERSION

This document describes version 0.730 of pmhtml (from Perl distribution App-PMUtils), released on 2020-01-31.

=head1 SYNOPSIS

Basic usage:

 % pmhtml Some::Module

=head1 DESCRIPTION

This function renders a POD document using L<pod2html>, then open a browser
to show the resulting HTML document.

=head1 OPTIONS

C<*> marks required options.

=head2 Main options

=over

=item B<--module>=I<s>*

=item B<--no-pod>

=back

=head2 Output options

=over

=item B<--format>=I<s>

Choose output format, e.g. json, text.

Default value:

 undef

=item B<--json>

Set output format to json.

=item B<--naked-res>

When outputing as JSON, strip result envelope.

Default value:

 0

By default, when outputing as JSON, the full enveloped result is returned, e.g.:

    [200,"OK",[1,2,3],{"func.extra"=>4}]

The reason is so you can get the status (1st element), status message (2nd
element) as well as result metadata/extra result (4th element) instead of just
the result (3rd element). However, sometimes you want just the result, e.g. when
you want to pipe the result for more post-processing. In this case you can use
`--naked-res` so you just get:

    [1,2,3]


=back

=head2 Other options

=over

=item B<--help>, B<-h>, B<-?>

Display help message and exit.

=item B<--version>, B<-v>

Display program's version and exit.

=back

=head1 COMPLETION

This script has shell tab completion capability with support for several
shells.

=head2 bash

To activate bash completion for this script, put:

 complete -C pmhtml pmhtml

in your bash startup (e.g. F<~/.bashrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is recommended, however, that you install modules using L<cpanm-shcompgen>
which can activate shell completion for scripts immediately.

=head2 tcsh

To activate tcsh completion for this script, put:

 complete pmhtml 'p/*/`pmhtml`/'

in your tcsh startup (e.g. F<~/.tcshrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is also recommended to install L<shcompgen> (see above).

=head2 other shells

For fish and zsh, install L<shcompgen> as described above.

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-PMUtils>.

=head1 SOURCE

Source repository is at L<https://github.com/perlancar/perl-App-PMUtils>.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-PMUtils>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=head1 SEE ALSO

L<pmdoc>, L<pmman>

L<lcpan> (C<doc> subcommand with <--html> option)

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2020, 2019, 2018, 2017, 2016, 2015, 2014 by perlancar@cpan.org.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
