#!perl

use 5.010;
use strict;
use warnings;

use App::PMUtils;
use Perinci::CmdLine::Any;

our $AUTHORITY = 'cpan:PERLANCAR'; # AUTHORITY
our $DATE = '2022-08-12'; # DATE
our $DIST = 'App-PMUtils'; # DIST
our $VERSION = '0.740'; # VERSION

our %SPEC;
$SPEC{pmlist} = {
    v => 1.1,
    summary => 'List Perl modules (and PODs, and module prefixes)',
    description => <<'_',

This is basically a CLI interface to <pm:Module::List::More>, with tab
completion.

_
    args => {
        query => {
            schema  => 'str*',
            default => '',
            pos     => 0,
            completion => sub {
                require Complete::Module;
                my %args = @_;
                Complete::Module::complete_module(word=>$args{word});
            },
            tags => ['category:filtering'],
        },
        pm => {
            summary => 'List .pm files',
            schema  => 'bool',
            default => 1,
            tags => ['category:filtering'],
        },
        pod => {
            summary => 'List .pod files',
            schema  => 'bool',
            default => 0,
            tags => ['category:filtering'],
        },
        prefix => {
            summary => 'List prefixes (dirs)',
            schema  => 'bool',
            default => 0,
            tags => ['category:filtering'],
        },
        xs => {
            summary => 'Only list XS modules',
            'summary.alt.bool.not' => 'Only list PP (pure-perl) modules',
            schema  => 'bool',
            tags => ['category:filtering'],
            cmdline_aliases => {
                pp => {
                    summary => 'Alias for --no-core',
                    is_flag => 1,
                    code => sub { $_[0]{xs} = 0 },
                },
            },
        },
        core => {
            summary => 'Only list core modules',
            'summary.alt.bool.not' => 'Only list non-core modules',
            schema  => 'bool',
            tags => ['category:filtering'],
        },
        core_dirs => {
            summary => 'Only search in core directories',
            'summary.alt.bool.not' => 'Only search in non-core directories',
            schema => 'bool',
            tags => ['category:filtering'],
        },
        exclude_curdir => {
            summary => 'Do not include "." in search dir',
            schema => 'bool',
            description => <<'_',

Older perls still include "." in default `@INC`. This option removes "." from
`@INC`. In newer perls this option should be unnecessary.

_
            tags => ['category:filtering'],
        },
        recurse => {
            schema  => 'bool',
            cmdline_aliases => { R => {} },
        },
        detail => {
            summary => 'Return detailed records instead of just module names',
            schema  => 'bool',
            cmdline_aliases => { l=>{} },
        },
        with_path => {
            summary => 'Instead of module names, return module paths instead',
            schema => 'bool',
            cmdline_aliases => { x=>{} },
        },
        ls_mode => {
            schema => 'bool*',
            default => 1,
        },
    },
    args_rels => {
        choose_one => ['detail', 'with_path'],
    },
};
sub pmlist {
    require Module::List::More;
    require String::Wildcard::Bash;

    my %args = @_;

    require Module::CoreList::More if defined($args{core}) || $args{detail};
    require Module::XSOrPP if defined($args{xs}) || $args{detail};

    my $query = $args{query} // "";

    local @INC = @INC;
    @INC = do {
        no warnings 'once';
        require Config;
        @Config::Config{qw(privlibexp archlibexp)};
    } if $args{core_dirs};
    @INC = grep { $_ ne '.' } @INC if $args{exclude_curdir};

    my $mods = Module::List::More::list_modules(
        $query,
        {
            return_path   => 1,
            wildcard      => 1,
            list_prefixes => $args{prefix},
            list_modules  => $args{pm},
            list_pod      => $args{pod},
            recurse       => $args{recurse},
            ls_mode       => $args{ls_mode},
        });
    my @res;
  MOD:
    for my $mod (sort keys %$mods) {
        my $is_core;
        if (defined $args{core}) {
            $is_core = Module::CoreList::More->is_core($mod);
            next MOD if $args{core} xor $is_core;
        }
        my $is_xs;
        my $is_xs_calculated;
        if (defined $args{xs}) {
            $is_xs = Module::XSOrPP::is_xs($mod);
            $is_xs_calculated++;
            next MOD if $args{xs} xor $is_xs;
        }

        if ($args{detail}) {
            push @res, {
                module  => $mod,
                path    => $mods->{$mod}{module_path},
                is_core => $is_core // Module::CoreList::More->is_core($mod),
                is_xs   => $is_xs_calculated ? $is_xs : Module::XSOrPP::is_xs($mod),
            };
        } else {
            push @res, $args{with_path} ? $mods->{$mod} : $mod;
        }
    }
    my $meta = {};
    if ($args{detail}) {
        $meta->{'table.fields'} = [qw/module path is_core is_xs/];
    }
    [200, "OK", \@res, $meta];
}

Perinci::CmdLine::Any->new(
    url => '/main/pmlist',
    read_config => 0,
    read_env => 0,
)->run;

# ABSTRACT: List Perl modules (and PODs, and module prefixes)
# PODNAME: pmlist

__END__

=pod

=encoding UTF-8

=head1 NAME

pmlist - List Perl modules (and PODs, and module prefixes)

=head1 VERSION

This document describes version 0.740 of pmlist (from Perl distribution App-PMUtils), released on 2022-08-12.

=head1 SYNOPSIS

Basic usage:

 % pmlist Mo      ; # search Mo.pm
 % pmlist Mo::    ; # search one level deep
 % pmlist Mo:: -R ; # search recursively (multiple levels)

 % pmlist Mo:: -l ; # list detailed records e.g. path (much like 'ls -l' equivanet instead of just 'ls')

 % pmlist Type::Tiny -R --pod        ; # search .pm as well as .pod
 % pmlist Type::Tiny -R --pod --nopm ; # search .pod only

Wildcard:

 % pmlist Module::P*
 % pmlist Module/P**
 % pmlist **::Tiny

=head1 DESCRIPTION

This is basically a CLI interface to L<Module::List::More>, with tab
completion.

=head1 OPTIONS

C<*> marks required options.

=head2 Main options

=over

=item B<--detail>, B<-l>

Return detailed records instead of just module names.

=item B<--no-ls-mode>

=item B<--recurse>, B<-R>

=item B<--with-path>, B<-x>

Instead of module names, return module paths instead.

=back

=head2 Filtering options

=over

=item B<--core>

Only list core modules.

=item B<--core-dirs>

Only search in core directories.

=item B<--exclude-curdir>

Do not include "." in search dir.

Older perls still include "." in default C<@INC>. This option removes "." from
C<@INC>. In newer perls this option should be unnecessary.


=item B<--no-pm>

=item B<--pod>

List .pod files.

=item B<--pp>

Alias for --no-core.

See C<--xs>.

=item B<--prefix>

List prefixes (dirs).

=item B<--query>=I<s>

Default value:

 ""

Can also be specified as the 1st command-line argument.

=item B<--xs>

Only list XS modules.

=back

=head2 Output options

=over

=item B<--format>=I<s>

Choose output format, e.g. json, text.

Default value:

 undef

Output can be displayed in multiple formats, and a suitable default format is
chosen depending on the application and/or whether output destination is
interactive terminal (i.e. whether output is piped). This option specifically
chooses an output format.


=item B<--json>

Set output format to json.

=item B<--naked-res>

When outputing as JSON, strip result envelope.

Default value:

 0

By default, when outputing as JSON, the full enveloped result is returned, e.g.:

 [200,"OK",[1,2,3],{"func.extra"=>4}]

The reason is so you can get the status (1st element), status message (2nd
element) as well as result metadata/extra result (4th element) instead of just
the result (3rd element). However, sometimes you want just the result, e.g. when
you want to pipe the result for more post-processing. In this case you can use
C<--naked-res> so you just get:

 [1,2,3]


=item B<--page-result>

Filter output through a pager.

This option will pipe the output to a specified pager program. If pager program
is not specified, a suitable default e.g. C<less> is chosen.


=item B<--view-result>

View output using a viewer.

This option will first save the output to a temporary file, then open a viewer
program to view the temporary file. If a viewer program is not chosen, a
suitable default, e.g. the browser, is chosen.


=back

=head2 Other options

=over

=item B<--help>, B<-h>, B<-?>

Display help message and exit.

=item B<--version>, B<-v>

Display program's version and exit.

=back

=head1 COMPLETION

This script has shell tab completion capability with support for several
shells.

=head2 bash

To activate bash completion for this script, put:

 complete -C pmlist pmlist

in your bash startup (e.g. F<~/.bashrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is recommended, however, that you install modules using L<cpanm-shcompgen>
which can activate shell completion for scripts immediately.

=head2 tcsh

To activate tcsh completion for this script, put:

 complete pmlist 'p/*/`pmlist`/'

in your tcsh startup (e.g. F<~/.tcshrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is also recommended to install L<shcompgen> (see above).

=head2 other shells

For fish and zsh, install L<shcompgen> as described above.

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-PMUtils>.

=head1 SOURCE

Source repository is at L<https://github.com/perlancar/perl-App-PMUtils>.

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 CONTRIBUTING


To contribute, you can send patches by email/via RT, or send pull requests on
GitHub.

Most of the time, you don't need to build the distribution yourself. You can
simply modify the code, then test via:

 % prove -l

If you want to build the distribution (e.g. to try to install it locally on your
system), you can install L<Dist::Zilla>,
L<Dist::Zilla::PluginBundle::Author::PERLANCAR>,
L<Pod::Weaver::PluginBundle::Author::PERLANCAR>, and sometimes one or two other
Dist::Zilla- and/or Pod::Weaver plugins. Any additional steps required beyond
that are considered a bug and can be reported to me.

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2022, 2021, 2020, 2019, 2018, 2017, 2016, 2015, 2014 by perlancar <perlancar@cpan.org>.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-PMUtils>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=cut
