#!perl

our $DATE = '2020-01-31'; # DATE
our $VERSION = '0.730'; # VERSION

use 5.010;
use strict;
use warnings;

use App::PMUtils;
use Perinci::CmdLine::Any;

our %SPEC;
$SPEC{pmcat} = {
    v => 1.1,
    summary => 'Print Perl module source codes',
    args => {
        module => $App::PMUtils::arg_module_multiple,
        pod => {
            schema => 'bool',
            default => 0,
        },
    },
};
sub pmcat {
    require Module::Path::More;

    my %args = @_;
    my $mods = $args{module};
    return [400, "Please specify at least one module"] unless @$mods;

    my $has_success;
    my $has_error;
    for my $mod (@$mods) {
        $mod =~ s!/!::!g;
        my $path = Module::Path::More::module_path(module=>$mod, find_pod=>$args{pod}) or do {
            warn "pmcat: No such module '$mod'";
            $has_error++;
            next;
        };
        open my $fh, "<", $path or do {
            warn "pmcat: Can't open '$path': $!\n";
            $has_error++;
            next;
        };
        print while <$fh>;
        close $fh;
        $has_success++;
    }

    if ($has_error) {
        if ($has_success) {
            return [207, "Some modules failed"];
        } else {
            return [500, "All modules failed"];
        }
    } else {
        return [200, "All modules OK"];
    }
}

Perinci::CmdLine::Any->new(
    url => '/main/pmcat',
    read_config => 0,
    read_env => 0,
)->run;

# ABSTRACT: Print Perl module source codes
# PODNAME: pmcat

__END__

=pod

=encoding UTF-8

=head1 NAME

pmcat - Print Perl module source codes

=head1 VERSION

This document describes version 0.730 of pmcat (from Perl distribution App-PMUtils), released on 2020-01-31.

=head1 SYNOPSIS

Basic usage:

 % pmcat Some::Module Another::Module

=head1 OPTIONS

C<*> marks required options.

=head2 Main options

=over

=item B<--module-json>=I<s>

See C<--module>.

=item B<--module>=I<s@>*

Can be specified multiple times.

=item B<--pod>

=back

=head2 Output options

=over

=item B<--format>=I<s>

Choose output format, e.g. json, text.

Default value:

 undef

=item B<--json>

Set output format to json.

=item B<--naked-res>

When outputing as JSON, strip result envelope.

Default value:

 0

By default, when outputing as JSON, the full enveloped result is returned, e.g.:

    [200,"OK",[1,2,3],{"func.extra"=>4}]

The reason is so you can get the status (1st element), status message (2nd
element) as well as result metadata/extra result (4th element) instead of just
the result (3rd element). However, sometimes you want just the result, e.g. when
you want to pipe the result for more post-processing. In this case you can use
`--naked-res` so you just get:

    [1,2,3]


=back

=head2 Other options

=over

=item B<--help>, B<-h>, B<-?>

Display help message and exit.

=item B<--version>, B<-v>

Display program's version and exit.

=back

=head1 COMPLETION

This script has shell tab completion capability with support for several
shells.

=head2 bash

To activate bash completion for this script, put:

 complete -C pmcat pmcat

in your bash startup (e.g. F<~/.bashrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is recommended, however, that you install modules using L<cpanm-shcompgen>
which can activate shell completion for scripts immediately.

=head2 tcsh

To activate tcsh completion for this script, put:

 complete pmcat 'p/*/`pmcat`/'

in your tcsh startup (e.g. F<~/.tcshrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is also recommended to install L<shcompgen> (see above).

=head2 other shells

For fish and zsh, install L<shcompgen> as described above.

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-PMUtils>.

=head1 SOURCE

Source repository is at L<https://github.com/perlancar/perl-App-PMUtils>.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-PMUtils>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2020, 2019, 2018, 2017, 2016, 2015, 2014 by perlancar@cpan.org.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
