#!/usr/bin/env perl

use 5.006002;

use strict;
use warnings;

use Astro::SpaceTrack;
use Getopt::Long 2.33 qw{ :config auto_version };
use JSON;
use Pod::Usage;

our $VERSION = '0.100';

my %opt;

GetOptions( \%opt,
    qw{ identity! password=s tle! username=s },
    help => sub { pod2usage( { -verbose => 2 } ) },
) or pod2usage( { -verbose => 0 } );

my $st = Astro::SpaceTrack->new(
    map { defined $opt{$_} ? ( $_ => $opt{$_} ) : () }
    qw{ identity username password }
);

if ( $opt{tle} ) {

    # Astro::Coord::ECI::TLE format

    my ( $resp, $data ) = $st->iridium_status(
	{ raw	=> 0 },
	'kelso',
    );

    $resp->is_success()
	or die "Failed to retrieve Iridium status: ", $resp->status_line();

    print <<"EOD";
# All Iridium Classic satellites.
#
# Generated by Astro::SpaceTrack tools/all_iridium_classic -tle
# on @{[ scalar gmtime ]} GMT

%status = (
EOD

    foreach my $body ( @{ $data } ) {
	print <<"EOD";
    '$body->[0]'	=> {
	'class'	=> 'Astro::Coord::ECI::TLE::Iridium',
	'comment' => '$body->[3]',
	'id'	=> '$body->[0]',
	'name'	=> '$body->[1]',
	'status' => $body->[4],
	'type'	=> 'iridium'
    },
EOD
    }

    print ");\n";

} else {

    # Astro::SpaceTrack format

    my ( $resp, $data ) = $st->iridium_status(
	{ raw	=> 1 },
	'spacetrack',
    );

    $resp->is_success()
	or die "Failed to retrieve Iridium status: ", $resp->status_line();

    print <<"EOD";
# All Iridium Classic satellites. The order of the data is:
# OID, name, status string, comment, portable status.
#
# Generated by tools/all_iridium_classic
# on @{[ scalar gmtime ]} GMT

my \@all_iridium_classic = (
EOD

    foreach my $body ( @{ $data } ) {
	print <<"EOD";
    [ $body->[0], '$body->[1]', '$body->[2]', '$body->[3]', $body->[4] ],
EOD
    }

    print ");\n";
}

__END__

=head1 TITLE

all_iridium_classic - Dump raw Space Track status for original-design Iridium satellites.

=head1 SYNOPSIS

 all_iridium_classic -identity
 all_iridium_classic -username somebody -password censored
 all_iridium_classic -help
 all_iridium_classic -version

=head1 OPTIONS

=head2 -help

This option displays the documentation for this script. The script then
exits.

=head2 -identity

If asserted, this Boolean option causes the
L<Astro::SpaceTrack|Astro::SpaceTrack> C<identity> attribute to be set,
which in turn causes the user's Space Track username and password to be
read from a (hopefully encrypted) identity file.

The default is C<-noidentity>.

=head2 -password

 -password censored

This option specifies the user's Space Track password. It overrides the
password from the identity file, if any.

=head2 -username

 -username somebody

This option specifies the user's Space Track username. It overrides the
username from the identity file, if any.

=head2 -version

This option displays the version of this script. The script then exits.

=head1 DETAILS

This Perl script does a raw query of Iridium status from Space Track,
formats the results as Perl code, and writes them to standard out. The
raw data are not supplimented from Celestrak, so the only statuses are
unknown or decayed.

The purpose of this is to generate a canned status table in
L<Astro::SpaceTrack|Astro::SpaceTrack>, since all the other sources
typically drop the satellite when it decays.

You need a Space Track username and password to use this script.

=head1 AUTHOR

Tom Wyant (wyant at cpan dot org)

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2017 by Thomas R. Wyant, III

This program is free software; you can redistribute it and/or modify it
under the same terms as Perl 5.10.0. For more details, see the full text
of the licenses in the directory LICENSES.

This program is distributed in the hope that it will be useful, but
without any warranty; without even the implied warranty of
merchantability or fitness for a particular purpose.

=cut

# ex: set textwidth=72 :
