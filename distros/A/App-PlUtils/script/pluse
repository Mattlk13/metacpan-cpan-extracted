#!perl

our $DATE = '2017-08-16'; # DATE
our $VERSION = '0.120'; # VERSION

use 5.010;
use strict;
use warnings;

use App::PlUtils;
use Perinci::CmdLine::Any;

our %SPEC;
$SPEC{pluse} = {
    v => 1.1,
    summary => 'List required modules (via use()/require()) in a Perl script (or module)',
    description => <<'_',



_
    args => {
        module => $App::PlUtils::arg_module_single,
        file => {
            summary => 'Specify file instead of module',
            schema  => 'filename*',
            pos     => 0,
        },
        detail => {
            schema => 'bool',
        },
    },
    result_naked=>1,
};
sub pluse {
    require Compiler::Lexer;
    require Module::Path::More;

    my %args = @_;
    my $path;
    if (defined $args{module}) {
        my $mod = $args{module};
        $mod =~ s/\.pm$//; $mod =~ s!/!::!g; # convenience
        $path = Module::Path::More::module_path(module=>$mod);
        die "Can't find module '$mod'" unless $path;
    } elsif (defined $args{file}) {
        $path = $args{file};
        (-f $path) or die "No such file '$path'\n";
    } else {
        die "Please specify file or module (-m)\n";
    }

    my $lexer = Compiler::Lexer->new($path);
    my $ct = do {
        undef $/;
        open my $fh, "<", $path or die "Can't open '$path': $!\n";
        my $res = <$fh>;
        close $fh;
        $res;
    };
    my $tokens = $lexer->tokenize($ct);
    my @res;
    for my $i (0..@$tokens-1) {
        my $t = $tokens->[$i];
        my $mod;
        if ($i < @$tokens-1 && (
            $t->{name} eq 'UseDecl' || $t->{name} eq 'RequireDecl')) {
            my $t2 = $tokens->[$i+1];
            if ($t2->{name} eq 'UsedName' || $t2->{name} eq 'RequiredName') {
                $mod = $t2->{data};
            } elsif ($t2->{name} eq 'Namespace') {
                my $j = $i+1;
                my $s = "";
                while ($t2 && $t2->{name} =~ /^Namespace(Resolver)?$/) {
                    $s .= $t2->{data};
                    $j++;
                    $t2 = $tokens->[$j];
                }
                $mod = $s;
            } else {
                next;
            }
            if ($args{detail}) {
                push @res, {
                    module=>$mod,
                    line=>$t->{line},
                    method=>$t->{name} eq 'UseDecl' ? 'use' : 'require',
                };
            } else {
                push @res, $mod;
            }
        }
    }
    \@res;
}

Perinci::CmdLine::Any->new(
    url => '/main/pluse',
)->run;

# ABSTRACT: List required modules (via use()/require()) in a Perl script (or module)
# PODNAME: pluse

__END__

=pod

=encoding UTF-8

=head1 NAME

pluse - List required modules (via use()/require()) in a Perl script (or module)

=head1 VERSION

This document describes version 0.120 of pluse (from Perl distribution App-PlUtils), released on 2017-08-16.

=head1 SYNOPSIS

 % pluse Some/script.pl
 % pluse --module Some::Module

Sample output:

 % cat 1.pl
 require A::B;
 use C;

 % pluse 1.pl
 +------+
 | A::B |
 | C    |
 +------+

 % pluse 1.pl --detail
 +------+---------+--------+
 | line | method  | module |
 | 1    | require | A::B   |
 | 2    | use     | C      |
 +------+---------+--------+

=head1 DESCRIPTION

=head1 OPTIONS

C<*> marks required options.

=head2 Main options

=over

=item B<--detail>

=item B<--file>=I<s>

Specify file instead of module.

=item B<--module>=I<s>, B<-m>

=back

=head2 Configuration options

=over

=item B<--config-path>=I<filename>

Set path to configuration file.

Can be specified multiple times.

=item B<--config-profile>=I<s>

Set configuration profile to use.

=item B<--no-config>

Do not use any configuration file.

=back

=head2 Environment options

=over

=item B<--no-env>

Do not read environment for default options.

=back

=head2 Output options

=over

=item B<--format>=I<s>

Choose output format, e.g. json, text.

Default value:

 undef

=item B<--json>

Set output format to json.

=item B<--naked-res>

When outputing as JSON, strip result envelope.

Default value:

 0

By default, when outputing as JSON, the full enveloped result is returned, e.g.:

    [200,"OK",[1,2,3],{"func.extra"=>4}]

The reason is so you can get the status (1st element), status message (2nd
element) as well as result metadata/extra result (4th element) instead of just
the result (3rd element). However, sometimes you want just the result, e.g. when
you want to pipe the result for more post-processing. In this case you can use
`--naked-res` so you just get:

    [1,2,3]


=back

=head2 Other options

=over

=item B<--help>, B<-h>, B<-?>

Display help message and exit.

=item B<--version>, B<-v>

Display program's version and exit.

=back

=head1 COMPLETION

This script has shell tab completion capability with support for several
shells.

=head2 bash

To activate bash completion for this script, put:

 complete -C pluse pluse

in your bash startup (e.g. F<~/.bashrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is recommended, however, that you install modules using L<cpanm-shcompgen>
which can activate shell completion for scripts immediately.

=head2 tcsh

To activate tcsh completion for this script, put:

 complete pluse 'p/*/`pluse`/'

in your tcsh startup (e.g. F<~/.tcshrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is also recommended to install L<shcompgen> (see above).

=head2 other shells

For fish and zsh, install L<shcompgen> as described above.

=head1 CONFIGURATION FILE

This script can read configuration files. Configuration files are in the format of L<IOD>, which is basically INI with some extra features.

By default, these names are searched for configuration filenames (can be changed using C<--config-path>): F<~/.config/pluse.conf>, F<~/pluse.conf>, or F</etc/pluse.conf>.

All found files will be read and merged.

To disable searching for configuration files, pass C<--no-config>.

You can put multiple profiles in a single file by using section names like C<[profile=SOMENAME]> or C<[SOMESECTION profile=SOMENAME]>. Those sections will only be read if you specify the matching C<--config-profile SOMENAME>.

You can also put configuration for multiple programs inside a single file, and use filter C<program=NAME> in section names, e.g. C<[program=NAME ...]> or C<[SOMESECTION program=NAME]>. The section will then only be used when the reading program matches.

Finally, you can filter a section by environment variable using the filter C<env=CONDITION> in section names. For example if you only want a section to be read if a certain environment variable is true: C<[env=SOMEVAR ...]> or C<[SOMESECTION env=SOMEVAR ...]>. If you only want a section to be read when the value of an environment variable has value equals something: C<[env=HOSTNAME=blink ...]> or C<[SOMESECTION env=HOSTNAME=blink ...]>. If you only want a section to be read when the value of an environment variable does not equal something: C<[env=HOSTNAME!=blink ...]> or C<[SOMESECTION env=HOSTNAME!=blink ...]>. If you only want a section to be read when an environment variable contains something: C<[env=HOSTNAME*=server ...]> or C<[SOMESECTION env=HOSTNAME*=server ...]>. Note that currently due to simplistic parsing, there must not be any whitespace in the value being compared because it marks the beginning of a new section filter or section name.

List of available configuration parameters:

 detail (see --detail)
 file (see --file)
 format (see --format)
 module (see --module)
 naked_res (see --naked-res)

=head1 ENVIRONMENT

=head2 PLUSE_OPT => str

Specify additional command-line options.

=head1 FILES

F<~/.config/pluse.conf>

F<~/pluse.conf>

F</etc/pluse.conf>

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-PlUtils>.

=head1 SOURCE

Source repository is at L<https://github.com/perlancar/perl-App-PlUtils>.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-PlUtils>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2017, 2016, 2015, 2014 by perlancar@cpan.org.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
