#!perl

our $DATE = '2020-04-30'; # DATE
our $VERSION = '0.121'; # VERSION

use 5.010;
use strict;
use warnings;
use Log::ger;

use Perinci::Object;
use Perinci::CmdLine::Any;

our %SPEC;
$SPEC{update_fatpack_snippets} = {
    v => 1.1,
    summary => 'Update fatpack snippets in a module source code',
    description => <<'_',

This routine will try to find blocks of text enclosed by:

    # BEGIN FATPACK CODE: Foo/Bar.pm Baz.pm
    ...
    # END OF FATPACK CODE

(which is the snippet generated by <pm:Module::FatPack>) and try to run
`Module::FatPack::fatpack_modules()` again for those specified modules, then
replace the snippets with the newly generated ones.

_
    args => {
        filenames => {
            'x.name.is_plural' => 1,
            'x.name.singular' => 'filename',
            summary => 'Path to module source code (.pm files)',
            schema => ['array*', of=>'filename*', min_len=>1],
            req => 1,
            pos => 0,
            greedy => 1,
        },
    },
};
sub update_fatpack_snippets {
    require File::Slurper;
    require Module::FatPack;

    my %args = @_;

    my $filenames = $args{filenames};

    my $envres = envresmulti();

    for my $filename (@$filenames) {
        unless (-f $filename) {
            log_warn("File %s does not exist, skipped", $filename);
            $envres->add_result(404, "File does not exist", {item_id=>$filename});
            next;
        }
        my $content = File::Slurper::read_text($filename);
        my $orig_content = $content;

        my $code_gen_snippet = sub {
            my @mod_pms = @_;

            my $res = Module::FatPack::fatpack_modules(
                module_names => \@mod_pms,
                pm => 1,
                stripper => 1,
            );

            die "Can't generate fatpack code for [" . join(" ", @mod_pms) .
                "]: $res->[0] - $res->[1]"
                    unless $res->[0] == 200;

            # remove extraneous newlines
            $res->[2] =~ s/^(# END OF FATPACK CODE\n)\n+/$1/m;

            $res->[2];
        };

        $content =~ s!^# BEGIN FATPACK CODE: ([^\n]+).+?^# END OF FATPACK CODE\n!$code_gen_snippet->(split /\s+/, $1)!egms;

        if ($content eq $orig_content) {
            $envres->add_result(304, "Not modified", {item_id=>$filename});
            next;
        }

        File::Slurper::write_text($filename, $content);
        $envres->add_result(200, "Updated", {item_id=>$filename});
    }

    $envres->as_struct;
}

Perinci::CmdLine::Any->new(
    url => '/main/update_fatpack_snippets',
)->run;

# ABSTRACT: Update fatpack snippets in a module source code
# PODNAME: update-fatpack-snippets

__END__

=pod

=encoding UTF-8

=head1 NAME

update-fatpack-snippets - Update fatpack snippets in a module source code

=head1 VERSION

This document describes version 0.121 of update-fatpack-snippets (from Perl distribution App-FatPackUtils), released on 2020-04-30.

=head1 SYNOPSIS

Usage:

 % update-fatpack-snippets [--config-path=path | -c]
     [--config-profile=profile | -P] [--filename=s+] [--filenames-json=s]
     [--format=name] [--json] [--(no)naked-res] [--no-config | -C]
     [--no-env] [--page-result[=program]] <filename> ...

=head1 DESCRIPTION

This routine will try to find blocks of text enclosed by:

 # BEGIN FATPACK CODE: Foo/Bar.pm Baz.pm
 ...
 # END OF FATPACK CODE

(which is the snippet generated by L<Module::FatPack>) and try to run
C<Module::FatPack::fatpack_modules()> again for those specified modules, then
replace the snippets with the newly generated ones.

=head1 OPTIONS

C<*> marks required options.

=head2 Main options

=over

=item B<--filename>=I<s@>*

Path to module source code (.pm files).

Can be specified multiple times.

=item B<--filenames-json>=I<s>

Path to module source code (.pm files) (JSON-encoded).

See C<--filename>.

=back

=head2 Configuration options

=over

=item B<--config-path>=I<s>, B<-c>

Set path to configuration file.

=item B<--config-profile>=I<s>, B<-P>

Set configuration profile to use.

=item B<--no-config>, B<-C>

Do not use any configuration file.

=back

=head2 Environment options

=over

=item B<--no-env>

Do not read environment for default options.

=back

=head2 Output options

=over

=item B<--format>=I<s>

Choose output format, e.g. json, text.

Default value:

 undef

=item B<--json>

Set output format to json.

=item B<--naked-res>

When outputing as JSON, strip result envelope.

Default value:

 0

By default, when outputing as JSON, the full enveloped result is returned, e.g.:

    [200,"OK",[1,2,3],{"func.extra"=>4}]

The reason is so you can get the status (1st element), status message (2nd
element) as well as result metadata/extra result (4th element) instead of just
the result (3rd element). However, sometimes you want just the result, e.g. when
you want to pipe the result for more post-processing. In this case you can use
`--naked-res` so you just get:

    [1,2,3]


=item B<--page-result>

Filter output through a pager.

=back

=head2 Other options

=over

=item B<--help>, B<-h>, B<-?>

Display help message and exit.

=item B<--version>, B<-v>

Display program's version and exit.

=back

=head1 COMPLETION

This script has shell tab completion capability with support for several
shells.

=head2 bash

To activate bash completion for this script, put:

 complete -C update-fatpack-snippets update-fatpack-snippets

in your bash startup (e.g. F<~/.bashrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is recommended, however, that you install modules using L<cpanm-shcompgen>
which can activate shell completion for scripts immediately.

=head2 tcsh

To activate tcsh completion for this script, put:

 complete update-fatpack-snippets 'p/*/`update-fatpack-snippets`/'

in your tcsh startup (e.g. F<~/.tcshrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is also recommended to install L<shcompgen> (see above).

=head2 other shells

For fish and zsh, install L<shcompgen> as described above.

=head1 CONFIGURATION FILE

This script can read configuration files. Configuration files are in the format of L<IOD>, which is basically INI with some extra features.

By default, these names are searched for configuration filenames (can be changed using C<--config-path>): F<~/.config/update-fatpack-snippets.conf>, F<~/update-fatpack-snippets.conf>, or F</etc/update-fatpack-snippets.conf>.

All found files will be read and merged.

To disable searching for configuration files, pass C<--no-config>.

You can put multiple profiles in a single file by using section names like C<[profile=SOMENAME]> or C<[SOMESECTION profile=SOMENAME]>. Those sections will only be read if you specify the matching C<--config-profile SOMENAME>.

You can also put configuration for multiple programs inside a single file, and use filter C<program=NAME> in section names, e.g. C<[program=NAME ...]> or C<[SOMESECTION program=NAME]>. The section will then only be used when the reading program matches.

Finally, you can filter a section by environment variable using the filter C<env=CONDITION> in section names. For example if you only want a section to be read if a certain environment variable is true: C<[env=SOMEVAR ...]> or C<[SOMESECTION env=SOMEVAR ...]>. If you only want a section to be read when the value of an environment variable has value equals something: C<[env=HOSTNAME=blink ...]> or C<[SOMESECTION env=HOSTNAME=blink ...]>. If you only want a section to be read when the value of an environment variable does not equal something: C<[env=HOSTNAME!=blink ...]> or C<[SOMESECTION env=HOSTNAME!=blink ...]>. If you only want a section to be read when an environment variable contains something: C<[env=HOSTNAME*=server ...]> or C<[SOMESECTION env=HOSTNAME*=server ...]>. Note that currently due to simplistic parsing, there must not be any whitespace in the value being compared because it marks the beginning of a new section filter or section name.

List of available configuration parameters:

 filenames (see --filename)
 format (see --format)
 naked_res (see --naked-res)

=head1 ENVIRONMENT

=head2 UPDATE_FATPACK_SNIPPETS_OPT => str

Specify additional command-line options.

=head1 FILES

F<~/.config/update-fatpack-snippets.conf>

F<~/update-fatpack-snippets.conf>

F</etc/update-fatpack-snippets.conf>

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-FatPackUtils>.

=head1 SOURCE

Source repository is at L<https://github.com/perlancar/perl-App-FatPackUtils>.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-FatPackUtils>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2020, 2019, 2017, 2016, 2015 by perlancar@cpan.org.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
