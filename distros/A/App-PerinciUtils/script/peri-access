#!perl

our $DATE = '2017-07-10'; # DATE
our $VERSION = '0.10'; # VERSION

use 5.010;
use strict;
use warnings;

use Perinci::CmdLine::Any;

our %SPEC;

$SPEC{request_riap} = {
    v => 1.1,
    summary => 'Command-line Riap client',
    args => {
        action => {
            summary => 'Riap action, e.g. call, meta, info',
            description => <<'_',

This is the first argument for Perinci::Access->request().

_
            schema => ['str*', match=>qr/\A\w+\z/],
            req => 1,
            pos => 0,
        },
        url => {
            summary => 'Riap URL e.g. /Some/Module/ or http://example.org/api/',
            description => <<'_',

This is the second argument for Perinci::Access->request().

_
            schema => 'str*',
            req => 1,
            pos => 1,
        },
        extra => {
            summary => 'Additional Riap request keys',
            description => <<'_',

This is the third argument for Perinci::Access->request().

_
            schema => 'hash*',
        },
        copts => {
            summary => 'Options for Perinci::Access clients',
            description => <<'_',

This is the fourth argument for Perinci::Access->request().

_
            schema => 'hash*',
        },
        args => {
            summary => "Specify Riap request key 'args'",
            description => <<'_',

Can also be specified through declaring `args` key in `extra` hash.

_
            schema => 'hash*',
        },
        detail => {
            summary => "Set Riap request key 'detail' => 1",
            description => <<'_',

Can also be specified through declaring `detail` key in `extra` hash.

_
            schema => 'bool',
        },
    },
};
sub request_riap {
    require Perinci::Access;

    my %args = @_;
    # XXX schema
    my $action = $args{action} or return [400, "Please specify action"];
    my $url    = $args{url}    or return [400, "Please specify url"];

    my $pa = Perinci::Access->new;
    my $extra = {};

    if ($args{extra}) {
        for (keys %{$args{extra}}) {
            $extra->{$_} = $args{extra}{$_};
        }
    }
    if ($args{detail}) {
        $extra->{detail} //= 1;
    }
    if ($args{args}) {
        $extra->{args} //= {};
        for (keys %{ $args{args} }) {
            $extra->{args}{$_} = $args{args}{$_};
        }
    }

    my $copts = $args{copts} // {};

    $pa->request($action => $url, $extra, $copts);
}

$ENV{LOG} //= 0; # speed up startup, but allow overriding
my $cmd = Perinci::CmdLine::Any->new(
    url => '/main/request_riap',
);
$cmd->run;

1;
# ABSTRACT: Command-line Riap client
# PODNAME: peri-access

__END__

=pod

=encoding UTF-8

=head1 NAME

peri-access - Command-line Riap client

=head1 VERSION

This document describes version 0.10 of peri-access (from Perl distribution App-PerinciUtils), released on 2017-07-10.

=head1 SYNOPSIS

From command-line:

 % peri-access meta /Some/Module/
 % peri-access call /Some/Module/func --args '{"foo": "bar", "baz": 2}'

=head1 DESCRIPTION

This script is a command-line interface for L<Perinci::Access>.

=head1 OPTIONS

C<*> marks required options.

=head2 Main options

=over

=item B<--action>=I<s>*

Riap action, e.g. call, meta, info.

This is the first argument for Perinci::Access->request().


=item B<--args-json>=I<s>

Specify Riap request key 'args' (JSON-encoded).

See C<--args>.

=item B<--args>=I<s>

Specify Riap request key 'args'.

Can also be specified through declaring `args` key in `extra` hash.


=item B<--copts-json>=I<s>

Options for Perinci::Access clients (JSON-encoded).

See C<--copts>.

=item B<--copts>=I<s>

Options for Perinci::Access clients.

This is the fourth argument for Perinci::Access->request().


=item B<--detail>

Set Riap request key 'detail' => 1.

Can also be specified through declaring `detail` key in `extra` hash.


=item B<--extra-json>=I<s>

Additional Riap request keys (JSON-encoded).

See C<--extra>.

=item B<--extra>=I<s>

Additional Riap request keys.

This is the third argument for Perinci::Access->request().


=item B<--url>=I<s>*

Riap URL e.g. /Some/Module/ or http://example.org/api/.

This is the second argument for Perinci::Access->request().


=back

=head2 Configuration options

=over

=item B<--config-path>=I<filename>

Set path to configuration file.

Can be specified multiple times.

=item B<--config-profile>=I<s>

Set configuration profile to use.

=item B<--no-config>

Do not use any configuration file.

=back

=head2 Environment options

=over

=item B<--no-env>

Do not read environment for default options.

=back

=head2 Output options

=over

=item B<--format>=I<s>

Choose output format, e.g. json, text.

Default value:

 undef

=item B<--json>

Set output format to json.

=item B<--naked-res>

When outputing as JSON, strip result envelope.

Default value:

 0

By default, when outputing as JSON, the full enveloped result is returned, e.g.:

    [200,"OK",[1,2,3],{"func.extra"=>4}]

The reason is so you can get the status (1st element), status message (2nd
element) as well as result metadata/extra result (4th element) instead of just
the result (3rd element). However, sometimes you want just the result, e.g. when
you want to pipe the result for more post-processing. In this case you can use
`--naked-res` so you just get:

    [1,2,3]


=back

=head2 Other options

=over

=item B<--help>, B<-h>, B<-?>

Display help message and exit.

=item B<--version>, B<-v>

Display program's version and exit.

=back

=head1 COMPLETION

This script has shell tab completion capability with support for several
shells.

=head2 bash

To activate bash completion for this script, put:

 complete -C peri-access peri-access

in your bash startup (e.g. F<~/.bashrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is recommended, however, that you install modules using L<cpanm-shcompgen>
which can activate shell completion for scripts immediately.

=head2 tcsh

To activate tcsh completion for this script, put:

 complete peri-access 'p/*/`peri-access`/'

in your tcsh startup (e.g. F<~/.tcshrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is also recommended to install L<shcompgen> (see above).

=head2 other shells

For fish and zsh, install L<shcompgen> as described above.

=head1 CONFIGURATION FILE

This script can read configuration files. Configuration files are in the format of L<IOD>, which is basically INI with some extra features.

By default, these names are searched for configuration filenames (can be changed using C<--config-path>): F<~/.config/peri-access.conf>, F<~/peri-access.conf>, or F</etc/peri-access.conf>.

All found files will be read and merged.

To disable searching for configuration files, pass C<--no-config>.

You can put multiple profiles in a single file by using section names like C<[profile=SOMENAME]> or C<[SOMESECTION profile=SOMENAME]>. Those sections will only be read if you specify the matching C<--config-profile SOMENAME>.

You can also put configuration for multiple programs inside a single file, and use filter C<program=NAME> in section names, e.g. C<[program=NAME ...]> or C<[SOMESECTION program=NAME]>. The section will then only be used when the reading program matches.

Finally, you can filter a section by environment variable using the filter C<env=CONDITION> in section names. For example if you only want a section to be read if a certain environment variable is true: C<[env=SOMEVAR ...]> or C<[SOMESECTION env=SOMEVAR ...]>. If you only want a section to be read when the value of an environment variable has value equals something: C<[env=HOSTNAME=blink ...]> or C<[SOMESECTION env=HOSTNAME=blink ...]>. If you only want a section to be read when the value of an environment variable does not equal something: C<[env=HOSTNAME!=blink ...]> or C<[SOMESECTION env=HOSTNAME!=blink ...]>. If you only want a section to be read when an environment variable contains something: C<[env=HOSTNAME*=server ...]> or C<[SOMESECTION env=HOSTNAME*=server ...]>. Note that currently due to simplistic parsing, there must not be any whitespace in the value being compared because it marks the beginning of a new section filter or section name.

List of available configuration parameters:

 action (see --action)
 args (see --args)
 copts (see --copts)
 detail (see --detail)
 extra (see --extra)
 format (see --format)
 naked_res (see --naked-res)
 url (see --url)

=head1 ENVIRONMENT

=head2 PERI_ACCESS_OPT => str

Specify additional command-line options.

=head1 FILES

F<~/.config/peri-access.conf>

F<~/peri-access.conf>

F</etc/peri-access.conf>

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-PerinciUtils>.

=head1 SOURCE

Source repository is at L<https://github.com/perlancar/perl-App-PerinciUtils>.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-PerinciUtils>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=head1 SEE ALSO

L<riap> (from L<App::riap> distribution), a riap CLI shell.

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2017, 2016, 2015 by perlancar@cpan.org.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
