#!perl

our $DATE = '2017-03-04'; # DATE
our $VERSION = '0.008'; # VERSION

use 5.010001;
use strict;
use warnings;

use ExtUtils::MakeMaker;
use File::chdir;
use Perinci::CmdLine::Any;
use Perinci::Sub::Util qw(err);

our %SPEC;

$SPEC{gen_cpan_meta} = {
    v => 1.1,
    summary => 'Generate CPAN META for a distribution',
    description => <<'_',

NOTE: EARLY VERSION.

This utility assumes it is run in the top-level directory of a Perl distribution
and will try to generate a CPAN META structure for the distribution. For
example: `abstract` will be searched from POD, `name` from the shortest package
name, `version` from the main module, `prereqs` using <pm:App::ScanPrereqs> and
so on. Finally it will validate its generated META.json first before outputing
it, printing found errors to stderr.

This utility can be used to generate (at least the preliminary version of)
`META.json` for dists that do not yet have `META.json`.

_
    args => {
        dir => {
            summary => 'Instead of current dir, use this directory as the '.
                'top-level dist directory',
            schema => 'dirname*',
            pos => 0,
        },
        # XXX option to output to file (default META.json)
        # XXX option to overwrite existing file
    },
};
sub gen_cpan_meta {
    require App::ScanPrereqs;
    require CPAN::Meta::Validator;
    require File::Find::Wanted;
    require File::Slurper;
    require JSON::MaybeXS;

    my %args = @_;

    local $CWD = $args{dir} if defined $args{dir};

    my $meta = {};

    (-d "lib") or return [412, "lib/ directory expected"];
    my @lib_files =
        sort { length($a) <=> length($b) }
        File::Find::Wanted::find_wanted(
            sub { -f && /\.pm$/ }, "lib");

    @lib_files or return [412, "Can't find any .pm files under lib/"];

    # pick the shortest module to be the dist name
    my $pkg = $lib_files[0];
    $pkg =~ s!lib/!!; $pkg =~ s/\.pm$//; $pkg =~ s!/!::!g;

    # name
    {
        (my $name = $pkg) =~ s/::/-/g;
        $meta->{name} = $name;
    }

    # version
    {
        my $v = MM->parse_version($lib_files[0]);
        $v = undef if defined($v) && $v eq 'undef';
        defined $v or return [412, "Can't extract version from $lib_files[0]"];
        $meta->{version} = $v;
    }

    my $ct = File::Slurper::read_text($lib_files[0]);
    $ct =~ /^=head1 NAME\R\R\S+ - (.+)/m or last;

    # abstract
    {
        $meta->{abstract} = $1;
    }

    # meta-spec
    {
        $meta->{'meta-spec'} = {
            url => "http://search.cpan.org/perldoc?CPAN::Meta::Spec",
            version => 2,
        };
    }

    # generated by
    {
        $meta->{'generated_by'} = "gen-cpan-meta version " .
            ($main::VERSION // "dev");
    }

    # prereqs
    {
        my $res;

        $res = App::ScanPrereqs::scan_prereqs(
            scanner => "regular",
            files => [grep {-d} "lib", "bin", "script", "scripts"]);
        return err("Can't scan runtime prereqs", $res) unless $res->[0] == 200;
        my %runtime_prereqs;
        for my $e (@{ $res->[2] }) {
            $runtime_prereqs{$e->{module}} = $e->{version};
        }

        $res = App::ScanPrereqs::scan_prereqs(
            scanner => "regular",
            files => [grep {-d} "t", "xt"]);
        return err("Can't scan test prereqs", $res) unless $res->[0] == 200;
        my %test_prereqs;
        for my $e (@{ $res->[2] }) {
            my $v;
            if (defined $runtime_prereqs{$e->{module}}) {
                $v = version->parse($e->{version})
                    > version->parse($runtime_prereqs{$e->{module}}) ?
                    $e->{version} : $runtime_prereqs{$e->{module}};
;
                # use test version if larger
                $runtime_prereqs{$e->{module}} = $v;
            } else {
                $v = $e->{version};
                $test_prereqs{$e->{module}} = $v;
            }
        }
        $meta->{prereqs}{runtime}{requires} = \%runtime_prereqs;
        $meta->{prereqs}{test}{requires} = \%test_prereqs;
    }

    # author
    {
        my $author;
        if ($ct =~ /^=head1 AUTHORS?\R\R(.+)/m) {
            $author = $1;
        } else {
            $author = 'John Doe <john.doe@example.com>';
        }
        $meta->{author} = [$author];
    }

    # license (assumed to be perl5)
    {
        $meta->{license} = ["perl_5"];
    }

    # dynamic_config (assumed to be 0)
    {
        $meta->{dynamic_config} = 0;
    }

    # release_status (assumed to be stable)
    {
        $meta->{release_status} = 'stable';
    }

    # XXX resources

    # XXX x_authority?

    # let's validate it first
    {
        my $cmv = CPAN::Meta::Validator->new($meta);
        unless ($cmv->is_valid) {
            print "WARN: Error from validator: $_\n" for $cmv->errors;
        }
    }

    [200, "OK", JSON::MaybeXS->new->canonical(1)->pretty->encode($meta)];
}

Perinci::CmdLine::Any->new(
        url => '/main/gen_cpan_meta',
)->run;

# ABSTRACT: Generate CPAN META for a distribution
# PODNAME: gen-cpan-meta

__END__

=pod

=encoding UTF-8

=head1 NAME

gen-cpan-meta - Generate CPAN META for a distribution

=head1 VERSION

This document describes version 0.008 of gen-cpan-meta (from Perl distribution App-CPANMetaUtils), released on 2017-03-04.

=head1 SYNOPSIS

Usage:

 % gen-cpan-meta [options] [dir]

=head1 DESCRIPTION

NOTE: EARLY VERSION.

This utility assumes it is run in the top-level directory of a Perl distribution
and will try to generate a CPAN META structure for the distribution. For
example: C<abstract> will be searched from POD, C<name> from the shortest package
name, C<version> from the main module, C<prereqs> using L<App::ScanPrereqs> and
so on. Finally it will validate its generated META.json first before outputing
it, printing found errors to stderr.

This utility can be used to generate (at least the preliminary version of)
C<META.json> for dists that do not yet have C<META.json>.

=head1 OPTIONS

C<*> marks required options.

=head2 Main options

=over

=item B<--dir>=I<s>

Instead of current dir, use this directory as the top-level dist directory.

=back

=head2 Configuration options

=over

=item B<--config-path>=I<filename>

Set path to configuration file.

Can be specified multiple times.

=item B<--config-profile>=I<s>

Set configuration profile to use.

=item B<--no-config>

Do not use any configuration file.

=back

=head2 Environment options

=over

=item B<--no-env>

Do not read environment for default options.

=back

=head2 Output options

=over

=item B<--format>=I<s>

Choose output format, e.g. json, text.

Default value:

 undef

=item B<--json>

Set output format to json.

=item B<--naked-res>

When outputing as JSON, strip result envelope.

Default value:

 0

By default, when outputing as JSON, the full enveloped result is returned, e.g.:

    [200,"OK",[1,2,3],{"func.extra"=>4}]

The reason is so you can get the status (1st element), status message (2nd
element) as well as result metadata/extra result (4th element) instead of just
the result (3rd element). However, sometimes you want just the result, e.g. when
you want to pipe the result for more post-processing. In this case you can use
`--naked-res` so you just get:

    [1,2,3]


=back

=head2 Other options

=over

=item B<--help>, B<-h>, B<-?>

Display help message and exit.

=item B<--version>, B<-v>

Display program's version and exit.

=back

=head1 COMPLETION

This script has shell tab completion capability with support for several
shells.

=head2 bash

To activate bash completion for this script, put:

 complete -C gen-cpan-meta gen-cpan-meta

in your bash startup (e.g. F<~/.bashrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is recommended, however, that you install L<shcompgen> which allows you to
activate completion scripts for several kinds of scripts on multiple shells.
Some CPAN distributions (those that are built with
L<Dist::Zilla::Plugin::GenShellCompletion>) will even automatically enable shell
completion for their included scripts (using L<shcompgen>) at installation time,
so you can immediately have tab completion.

=head2 tcsh

To activate tcsh completion for this script, put:

 complete gen-cpan-meta 'p/*/`gen-cpan-meta`/'

in your tcsh startup (e.g. F<~/.tcshrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is also recommended to install L<shcompgen> (see above).

=head2 other shells

For fish and zsh, install L<shcompgen> as described above.

=head1 CONFIGURATION FILE

This script can read configuration files. Configuration files are in the format of L<IOD>, which is basically INI with some extra features.

By default, these names are searched for configuration filenames (can be changed using C<--config-path>): F<~/.config/gen-cpan-meta.conf>, F<~/gen-cpan-meta.conf>, or F</etc/gen-cpan-meta.conf>.

All found files will be read and merged.

To disable searching for configuration files, pass C<--no-config>.

You can put multiple profiles in a single file by using section names like C<[profile=SOMENAME]> or C<[SOMESECTION profile=SOMENAME]>. Those sections will only be read if you specify the matching C<--config-profile SOMENAME>.

You can also put configuration for multiple programs inside a single file, and use filter C<program=NAME> in section names, e.g. C<[program=NAME ...]> or C<[SOMESECTION program=NAME]>. The section will then only be used when the reading program matches.

Finally, you can filter a section by environment variable using the filter C<env=CONDITION> in section names. For example if you only want a section to be read if a certain environment variable is true: C<[env=SOMEVAR ...]> or C<[SOMESECTION env=SOMEVAR ...]>. If you only want a section to be read when the value of an environment variable has value equals something: C<[env=HOSTNAME=blink ...]> or C<[SOMESECTION env=HOSTNAME=blink ...]>. If you only want a section to be read when the value of an environment variable does not equal something: C<[env=HOSTNAME!=blink ...]> or C<[SOMESECTION env=HOSTNAME!=blink ...]>. If you only want a section to be read when an environment variable contains something: C<[env=HOSTNAME*=server ...]> or C<[SOMESECTION env=HOSTNAME*=server ...]>. Note that currently due to simplistic parsing, there must not be any whitespace in the value being compared because it marks the beginning of a new section filter or section name.

List of available configuration parameters:

 dir (see --dir)
 format (see --format)
 naked_res (see --naked-res)

=head1 ENVIRONMENT

=head2 GEN_CPAN_META_OPT => str

Specify additional command-line options.

=head1 FILES

F<~/.config/gen-cpan-meta.conf>

F<~/gen-cpan-meta.conf>

F</etc/gen-cpan-meta.conf>

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-CPANMetaUtils>.

=head1 SOURCE

Source repository is at L<https://github.com/perlancar/perl-App-ValidateCPANMetaWithSah>.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-CPANMetaUtils>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=head1 SEE ALSO

L<CPAN::Meta>

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2017, 2016 by perlancar@cpan.org.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
