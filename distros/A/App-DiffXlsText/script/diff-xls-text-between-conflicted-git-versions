#!perl

our $AUTHORITY = 'cpan:PERLANCAR'; # AUTHORITY
our $DATE = '2021-07-12'; # DATE
our $DIST = 'App-DiffXlsText'; # DIST
our $VERSION = '0.008'; # VERSION

use 5.010001;
use strict;
use warnings;
use Log::ger;
#use Log::ger::Screen;

use CLI::MetaUtil::Getopt::Long qw(GetOptionsCLIWrapper);
use File::Temp qw(tempdir);
use IPC::System::Options 'system', -log=>1, -die=>1;

# XXX duplication with diff-xls-text
my $opt_transpose_csv;

my $tempdir;
my $filename;

my $prog = "diff-xls-text-between-conflicted-git-versions";
my ($path_ours, $path_theirs);
GetOptionsCLIWrapper(
    cli => 'diff',
    add_opts => {
        'transpose-csv' => \$opt_transpose_csv,
        '<>' => sub {
            die "Please specify only one filename" if defined $filename;
            $filename = $_[0];
            (my $basename = $filename) =~ s!.+/!!;
            my ($content_ours, $content_theirs);
            system(
                {capture_stdout => \$content_ours},
                "git", "show", ":2:$filename",
            );
            system(
                {capture_stdout => \$content_theirs},
                "git", "show", ":3:$filename",
            );

            $tempdir = File::Temp::tempdir(CLEANUP => !$ENV{DEBUG});

            $path_ours = "$tempdir/ours.$basename";
            open my $fh_ours  , ">", $path_ours
                or die "$prog: Can't write ours: $!";
            print $fh_ours $content_ours;
            close $fh_ours
                or die "$prog: Can't write ours: $!";

            $path_theirs = "$tempdir/theirs.$basename";
            open my $fh_theirs, ">", $path_theirs
                or die "$prog: Can't write theirs: $!";
            print $fh_theirs $content_theirs;
            close $fh_theirs
                or die "$prog: Can't write theirs: $!";
        },
    },
);

die "Please specify file" unless defined $path_ours;
system(
    "diff-xls-text", @ARGV,
    ($opt_transpose_csv ? ("--transpose-csv") : ()),
    $path_ours, $path_theirs,
);

# ABSTRACT: Diff the text of two Office spreadsheets (.ods, .xls, .xlsx) as two directories of CSV files
# PODNAME: diff-xls-text-between-conflicted-git-versions

__END__

=pod

=encoding UTF-8

=head1 NAME

diff-xls-text-between-conflicted-git-versions - Diff the text of two Office spreadsheets (.ods, .xls, .xlsx) as two directories of CSV files

=head1 VERSION

This document describes version 0.008 of diff-xls-text-between-conflicted-git-versions (from Perl distribution App-DiffXlsText), released on 2021-07-12.

=head1 SYNOPSIS

 % diff-xls-text-between-conflicted-git-versions [options] <FILE>

=head1 DESCRIPTION

This is a wrapper for L<diff-xls-text> to compare two versions ("ours" and
"theirs") when there is a git merge conflict.

=head1 OPTIONS

See L<diff-xls-text>.

=head1 ENVIRONMENT

=head2 DEBUG

If set to true, do not cleanup temporary directories.

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/App-DiffXlsText>.

=head1 SOURCE

Source repository is at L<https://github.com/perlancar/perl-App-DiffXlsText>.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=App-DiffXlsText>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=head1 SEE ALSO

L<diff-xls-text>

Unix command L<diff>.

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2021 by perlancar@cpan.org.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
