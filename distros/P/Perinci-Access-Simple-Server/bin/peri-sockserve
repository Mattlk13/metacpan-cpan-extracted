#!perl

our $DATE = '2016-03-16'; # DATE
our $VERSION = '0.24'; # VERSION

use 5.010;
use strict;
use warnings;
use Log::Any qw($log);

use File::HomeDir;
use Module::List qw(list_modules);
use Module::Load;
use Perinci::Access::Simple::Server::Socket;
use Perinci::CmdLine::Any;
use Perinci::Gen::ForModule qw(gen_meta_for_module);

our %SPEC;

$SPEC{serve} = {
    v => 1.1,
    summary => 'Serve Perl modules using Riap::Simple protocol over sockets',
    description => <<'_',

This is a simple command-line front-end for
Perinci::Acces::Simple::Server::Socket, for making Perl modules accessible over
TCP or Unix socket, using the Riap::Simple protocol. First the specified Perl
modules will be loaded. Modules which do not contain Rinci metadata will be
equipped with metadata using Perinci::Sub::Gen::ForModule. Perl modules not
specified in the command-line arguments will not be accessible, since
Perinci::Access::Perl is used with load=>0.

Modules can be accessed with Riap clients such as Perinci::Access using URL:

 riap+tcp://HOST:PORT/MODULE/SUBMOD/FUNCTION
 riap+unix:UNIX_SOCKET_PATH//MODULE/SUBMOD/FUNCTION

_
    args => {
        modules => {
            schema => ['array*' => {of => 'str*', min_len => 1}],
            req => 1,
            pos => 0,
            greedy => 1,
            summary => 'List of modules to load',
            description => <<'_',

Either specify exact module name or one using wildcard (e.g. 'Foo::Bar::*', in
which Module::List will be used to load all modules under 'Foo::Bar::').

_
        },
        access_log_path => {
            schema => 'str',
            summary => 'Path for access log file',
            description => <<'_',

Default is ~/peri-sockserve-access.log

_
        },
        ports => {
            schema => 'str*',
            summary =>
                'Will be passed to Perinci::Access::Simple::Server::Socket',
            cmdline_aliases => {p=>{}},
        },
        unix_sockets => {
            schema => 'str*',
            summary =>
                'Will be passed to Perinci::Access::Simple::Server::Socket',
            cmdline_aliases => {s=>{}},
        },
        daemonize => {
            schema => ['bool' => {default=>0}],
            summary => 'If true, will daemonize into background',
            cmdline_aliases => {D=>{}},
        },
        fork => {
            schema => ['bool' => {default=>1}],
            summary => 'Set to false to disable forking',
        },
        library => {
            schema => ['array' => {
                of => 'str*',
            }],
            summary => 'Add directory to library search path, a la Perl\'s -I',
            cmdline_aliases => {I=>{}},
        },
    },
};
sub serve {
    $log->infof("Starting server ...");
    my %args = @_;

    return [400, "Please specify at least 1 module"]
        unless $args{modules} && @{$args{modules}};
    return [400, "Please specify at least port or Unix socket"]
        unless $args{ports} || $args{unix_sockets};

    my $access_log_path = $args{access_log_path} //
        File::HomeDir->my_home . "/peri-sockserve-access.log";

    for my $dir (@{ $args{library} // [] }) {
        require lib;
        lib->import($dir);
    }

    my @modules;
    for my $m (@{$args{modules}}) {
        if ($m =~ /(.+::)\*$/) {
            my $res = list_modules($1, {list_modules=>1});
            push @modules, keys %$res;
        } else {
            push @modules, $m;
        }
    }
    $log->debugf("Modules to load: %s", \@modules);
    for my $m (@modules) {
        $log->infof("Loading module %s ...", $m);
        eval { load $m };
        return [500, "Failed to load module $m: $@"] if $@;
        gen_meta_for_module(module=>$m, load=>0);
    }

    my $server = Perinci::Access::Simple::Server::Socket->new(
        ports           => $args{ports},
        unix_sockets    => $args{unix_sockets},
        daemonize       => $args{daemonize} // 0,
        scoreboard_path => 0,
        access_log_path => $access_log_path,
        start_servers   => ($args{fork} // 1) ? undef : 0,
    );
    $server->run;

    [200, "OK"];
}

Perinci::CmdLine::Any->new(url => '/main/serve')->run;

1;
# ABSTRACT: Serve Perl modules using Riap::Simple protocol over sockets
# PODNAME: peri-sockserve

__END__

=pod

=encoding UTF-8

=head1 NAME

peri-sockserve - Serve Perl modules using Riap::Simple protocol over sockets

=head1 VERSION

This document describes version 0.24 of peri-sockserve (from Perl distribution Perinci-Access-Simple-Server), released on 2016-03-16.

=head1 SYNOPSIS

 # serve modules
 % peri-sockserve -p 127.0.0.1:5678 -s /path/to/unix/sock Foo::Bar Baz::*

 # access the server
 % perl -MPerinci::Access -e'
     my $pa = Perinci::Access->new;
     my $res = $pa->request(call=>"riap+tcp://localhost:5678/Foo/Bar/func1",
                            {args=>{a1=>1, a2=>2}});'
     my $res = $pa->request(meta=>"riap+tcp:path/to/unix/sock//Baz/Qux/func2");'

 # see more options
 % peri-sockserve --help

=head1 DESCRIPTION

This is a simple command-line front-end for
Perinci::Acces::Simple::Server::Socket, for making Perl modules accessible over
TCP or Unix socket, using the Riap::Simple protocol. First the specified Perl
modules will be loaded. Modules which do not contain Rinci metadata will be
equipped with metadata using Perinci::Sub::Gen::ForModule. Perl modules not
specified in the command-line arguments will not be accessible, since
Perinci::Access::Perl is used with load=>0.

Modules can be accessed with Riap clients such as Perinci::Access using URL:

 riap+tcp://HOST:PORT/MODULE/SUBMOD/FUNCTION
 riap+unix:UNIX_SOCKET_PATH//MODULE/SUBMOD/FUNCTION

=head1 OPTIONS

C<*> marks required options.

=head2 Configuration options

=over

=item B<--config-path>=I<filename>

Set path to configuration file.

Can be specified multiple times.

=item B<--config-profile>=I<s>

Set configuration profile to use.

=item B<--no-config>

Do not use any configuration file.

=back

=head2 Environment options

=over

=item B<--no-env>

Do not read environment for default options.

=back

=head2 Output options

=over

=item B<--format>=I<s>

Choose output format, e.g. json, text.

Default value:

 undef

=item B<--json>

Set output format to json.

=item B<--naked-res>

When outputing as JSON, strip result envelope.

Default value:

 0

By default, when outputing as JSON, the full enveloped result is returned, e.g.:

    [200,"OK",[1,2,3],{"func.extra"=>4}]

The reason is so you can get the status (1st element), status message (2nd
element) as well as result metadata/extra result (4th element) instead of just
the result (3rd element). However, sometimes you want just the result, e.g. when
you want to pipe the result for more post-processing. In this case you can use
`--naked-res` so you just get:

    [1,2,3]


=back

=head2 Other options

=over

=item B<--access-log-path>=I<s>

Path for access log file.

Default is ~/peri-sockserve-access.log


=item B<--daemonize>, B<-D>

If true, will daemonize into background.

=item B<--help>, B<-h>, B<-?>

Display help message and exit.

=item B<--library-json>=I<s>, B<-I>

Add directory to library search path, a la Perl's -I (JSON-encoded).

See C<--library>.

=item B<--library>=I<s@>

Add directory to library search path, a la Perl's -I.

Can be specified multiple times.

=item B<--modules-json>=I<s>

List of modules to load (JSON-encoded).

See C<--modules>.

=item B<--modules>=I<s@>*

List of modules to load.

Either specify exact module name or one using wildcard (e.g. 'Foo::Bar::*', in
which Module::List will be used to load all modules under 'Foo::Bar::').


Can be specified multiple times.

=item B<--no-fork>

=item B<--ports>=I<s>, B<-p>

Will be passed to Perinci::Access::Simple::Server::Socket.

=item B<--unix-sockets>=I<s>, B<-s>

Will be passed to Perinci::Access::Simple::Server::Socket.

=item B<--version>, B<-v>

Display program's version and exit.

=back

=head1 COMPLETION

This script has shell tab completion capability with support for several
shells.

=head2 bash

To activate bash completion for this script, put:

 complete -C peri-sockserve peri-sockserve

in your bash startup (e.g. C<~/.bashrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is recommended, however, that you install L<shcompgen> which allows you to
activate completion scripts for several kinds of scripts on multiple shells.
Some CPAN distributions (those that are built with
L<Dist::Zilla::Plugin::GenShellCompletion>) will even automatically enable shell
completion for their included scripts (using C<shcompgen>) at installation time,
so you can immadiately have tab completion.

=head2 tcsh

To activate tcsh completion for this script, put:

 complete peri-sockserve 'p/*/`peri-sockserve`/'

in your tcsh startup (e.g. C<~/.tcshrc>). Your next shell session will then
recognize tab completion for the command. Or, you can also directly execute the
line above in your shell to activate immediately.

It is also recommended to install C<shcompgen> (see above).

=head2 other shells

For fish and zsh, install C<shcompgen> as described above.

=head1 CONFIGURATION FILE

This script can read configuration file, which by default is searched at ~/.config/peri-sockserve.conf, ~/peri-sockserve.conf or /etc/peri-sockserve.conf (can be changed by specifying C<--config-path>). All found files will be read and merged.

To disable searching for configuration files, pass C<--no-config>.

Configuration file is in the format of L<IOD>, which is basically INI with some extra features.

You can put multiple profiles in a single file by using section names like C<[profile=SOMENAME]> (filter by profile). Those sections will only be read if you specify the matching C<--config-profile SOMENAME>.

You can also put configuration for multiple programs inside a single file, and use filter C<program=NAME> in section names, e.g. C<[program=foo ...]>. The section will then only be used when the reading program matches.

Finally, you can filter a section by environment variable using the filter C<env=CONDITION> in section names. For example if you only want a section to be read if a certain environment variable is true: C<[env=SOMEVAR ...]>. If you only want a section to be read when the value of an environment variable has value equals something: C<[env=HOSTNAME=blink ...]>. If you only want a section to be read when the value of an environment variable does not equal something: C<[env=HOSTNAME!=blink ...]>. If you only want a section to be read when an environment variable contains something: C<[env=HOSTNAME*=server ...]>. Note that currently due to simplistic parsing, there must not be any whitespace in the value being compared because it marks the beginning of a new section filter or section name.

List of available configuration parameters:

 access_log_path (see --access-log-path)
 daemonize (see --daemonize)
 fork (see --no-fork)
 format (see --format)
 library (see --library)
 modules (see --modules)
 naked_res (see --naked-res)
 ports (see --ports)
 unix_sockets (see --unix-sockets)

=head1 ENVIRONMENT

=head2 PERI_SOCKSERVE_OPT => str

Specify additional command-line options

=head1 FILES

~/.config/peri-sockserve.conf

~/peri-sockserve.conf

/etc/peri-sockserve.conf

=head1 HOMEPAGE

Please visit the project's homepage at L<https://metacpan.org/release/Perinci-Access-Simple-Server>.

=head1 SOURCE

Source repository is at L<https://github.com/sharyanto/perl-Perinci-Access-Simple-Server>.

=head1 BUGS

Please report any bugs or feature requests on the bugtracker website L<https://rt.cpan.org/Public/Dist/Display.html?Name=Perinci-Access-Simple-Server>

When submitting a bug or request, please include a test-file or a
patch to an existing test-file that illustrates the bug or desired
feature.

=head1 SEE ALSO

L<Riap::Simple>, L<Riap>, L<Rinci>

L<Perinci::Access::Simple::Client>, L<Perinci::Access>

=head1 AUTHOR

perlancar <perlancar@cpan.org>

=head1 COPYRIGHT AND LICENSE

This software is copyright (c) 2016 by perlancar@cpan.org.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.

=cut
